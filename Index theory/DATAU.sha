*V indicates a value series in Australian Dollars;
*Q indicates a constant prices series, which we interpret as a quantity series. Except when noted, the base year for the constant price series 
*is 2010. The data points for 2018 are obtained by multiplying the last available OECD.STAT data point (2017) by the growth rate of chained volume 
*series from the Australian Bureau of Statistics (ABS). 
*As opposed to other countries on OECD.Stat, observations for Australian series are not for full calendar years (January-January) but rather
*financial years (July-July). For example, the observation "1959" should actually be "1959-1960", i.e. the two last quarters of 1959 and the two 
*first quarters of 1960.

*There are 61 years covering 1959 to 2019 so our annual series will be of length 60 and the capital stocks series of length 61 (beginning and end of year capital stocks).
SMPL 1 61
GENR C=0
GENR CHECK=0
GENR YEAR=TIME(1958)
*The above statement combined with the sample statement, SMPL 1 60,  generates a year series that starts with 1959 and ends with 2019.

GENR T=TIME(-1)
*This command generates a time series T that starts with 0 in 1959 and adds 1 to each subsequent year.
FORMAT(2F10.0)
PRINT YEAR T / FORMAT
*The above 2 commands print out the T variable. The FORMAT statement determines how many decimals the printed
*out series will have. FORMAT(f10.0) tells shazam that we are allocating 10 spaces for the series and the .0
*tells shazam that 0 of these spaces should be decimals. If I replaced the statement: FORMAT(f10.0) with
*FORMAT(f10.3), then the t series would be printed out with 3 decimal places. 

SMPL 1 62
GENR CON=0
GENR TOFT=0
GENR V=0
*Now we define values and quantities for the basic components of gdp (final expenditure gdp) c+i+g+x-m
GENR VC=0
GENR QC=0
GENR PC=0
*We will have 2 forms of investment; VI1=value of investment in fixed capital
*VI2=value of inventory change; essentially investment in inventories.
GENR VI1=0
GENR VI2=0
GENR QI1=0
GENR QI2=0
GENR PI1=0
GENR PI2=0
GENR VG=0
GENR QG=0
GENR PG=0
GENR VX=0
GENR PX=0
GENR QX=0
GENR VM=0
GENR QM=0
GENR PM=0
GENR PPX=0
GENR PPM=0
GENR VI=0
GENR QI=0
GENR PI=0
GENR QD=0
GENR VD=0
GENR PD=0

*This sample statement will allow us to read in the data from 1959 to 2018 for value (V) and quantity (Q) for the national accounts data from OECD.STAT
SMPL 1 60

*VC is P31S14_S15: Households and Non-profit institutions serving households
READ VC / BYVAR
9573.0	10182.0	10495.0	11212.0	12177.0	13202.0	14064.0	15209.0 
16566.0	18029.0	19951.0	21967.0	24306.0	27096.0	32273.0	39798.0 
46468.0	54523.0	60746.0	67246.0	75231.0	85280.0	98554.0	111825.0 
121312.0	129862.0	146710.0	162385.0	179402.0	200249.0	222792.0 
237046.0	248946.0	258202.0	268644.0	286336.0	304148.0	317553.0 
338589.0	361087.0	383899.0	413527.0	437246.0	468515.0	499805.0 
531474.0	565295.0	616412.0	666829.0	690568.0	731012.0	775116.0 
817502.0	853570.0	894930.0	929163.0	967568.0	1001197.0	1044303.0	1083475.0 

*CHECK TO SEE IF YOUR DATA HAS BEEN READ IN PROPERLY BY PRINTING IT OUT.
FORMAT(F8.0,F12.1)
*NOTE: THE FORMAT STATE SAYS THAT THE FIRST VARIABLE (YEAR) WILL BE ALLOCATE 8 SPACES WITH NO DECIMALS; THE SECOND VARIABLE
*WILL ALLOCATE 12 SPACES FOR THE VARIABLE WITH ONE DECIMAL PLACE
PRINT YEAR VC / FORMAT

*VG is P3S13: Final consumption expenditure of general government
READ VG / BYVAR
1843.0	1989.0	2129.0	2246.0	2416.0	2757.0	3122.0	3472.0	3973.0	4275.0	
4860.0	5613.0	6419.0	7278.0	8721.0	11965.0	14900.0	16794.0	18735.0	20618.0	
23173.0	27135.0	31196.0	35330.0	39141.0	44451.0	49452.0	54146.0	58502.0	63925.0	
69618.0	75433.0	80453.0	83941.0	85569.0	89143.0	94731.0	97852.0	103591.0	111662.0	
117601.0	125383.0	132328.0	140932.0	150642.0	161983.0	174174.0	188458.0	
203000.0	221679.0	234205.0	252311.0	269696.0	274083.0	282898.0	293438.0	
309995.0	328092.0	346455.0	366592.0


*VI is P51: Gross fixed capital formation (THIS IS BASICALLY INVESTMENT EXCLUDING CHANGE IN INVENTORIES)
READ VI1 / BYVAR
5028.0	5323.0	5299.0	5685.0	6386.0	7213.0	7868.0	8544.0	9488.0	10279.0	
11614.0	12371.0	13396.0	14483.0	17192.0	18418.0	21846.0	24807.0	27651.0	31685.0	
35548.0	43264.0	50916.0	50289.0	55635.0	63624.0	73407.0	79988.0	90110.0	105691.0	
111942.0	102039.0	96671.0	104330.0	111824.0	124434.0	127739.0	133813.0	
148246.0	157783.0	171882.0	164137.0	183780.0	209212.0	230112.0	249971.0	
277776.0	298373.0	332793.0	348181.0	352562.0	369114.0	411296.0	427996.0	
429754.0	425485.0	421390.0	422428.0	446596.0	458270.5

*VI2 is P52: Changes in inventories	
READ VI2 / BYVAR
217.0	501.0	-243.0	243.0	136.0	565.0	84.0	404.0	147.0	737.0	555.0	587.0	158.0	
-340.0	1260.0	683.0	180.0	1134.0	-528.0	1388.0	914.0	428.0	1550.0	-2364.0	
1271.0	2321.0	884.0	-1657.0	669.0	3356.0	5187.0	-1447.0	-2086.0	517.0	1430.0	
4386.0	3376.0	4547.0	2842.0	4783.0	2056.0	1411.0	960.0	-832.0	3849.0	
4224.0	-2717.0	1216.0	4679.0	-2951.0	-3172.0	6393.0	4692.0	569.0	-2217.0	
1076.0	1158.0	1943.0	2942.0	1609.0


*VX is P6: Exports of goods and services
READ VX / BYVAR
2155.0	2176.0	2476.0	2497.0	3169.0	3061.0	3148.0	3496.0	3586.0	3909.0	
4781.0	5103.0	5704.0	7040.0	7925.0	10160.0	11253.0	13469.0	14295.0	16968.0	
22093.0	22682.0	23774.0	25709.0	28970.0	35848.0	39059.0	44264.0	51780.0	55675.0	
61170.0	66571.0	70511.0	77916.0	83861.0	88611.0	99923.0	106476.0	115241.0	
113758.0	128494.0	156706.0	156693.0	152944.0	148127.0	168472.0	198229.0	
219531.0	237366.0	289894.0	257770.0	303673.0	322096.0	306588.0	336322.0	
324674.0	319721.0	373740.0	400666.0	466287.1


*VM is P7: Imports of goods and services
READ VM / BYVAR
2332.0	2636.0	2243.0	2656.0	2920.0	3535.0	3683.0	3770.0	4224.0	4360.0	
4871.0	5214.0	5325.0	5481.0	7964.0	10483.0	11137.0	14075.0	15308.0	18211.0	
21385.0	25469.0	29555.0	29553.0	32018.0	40663.0	47052.0	48834.0	53920.0	62414.0	
68953.0	67121.0	69360.0	79339.0	86262.0	98343.0	102318.0	104950.0	120412.0	
128932.0	142773.0	156098.0	156754.0	169812.0	171497.0	193754.0	216328.0	
237397.0	267352.0	287226.0	271168.0	289987.0	325825.0	326500.0	343157.0	
349444.0	357496.0	362889.0	393876.0	424493.2


*Now load the data for Q from OECD.STAT (1959-2018)
*QC is P31S14_S15: Households and Non-profit institutions serving households
READ QC / BYVAR
122914.0	125636.4	128787.3	135811.1	145374.8	152646.5	157130.4	165168.8	
173366.4	182594.6	193749.0	201266.7	208525.8	218262.2	231965.4	243278.2	
245116.7	258174.9	263284.2	267189.3	271961.3	280770.0	296440.5	303585.1	
306767.4	309497.8	325495.8	332072.3	342621.0	358497.3	373941.5	377557.7	
386369.9	393589.7	402046.7	419836.7	434128.6	447030.2	469154.7	496044.1	
517164.8	533611.9	550142.2	572321.2	601160.9	628483.3	648886.5	684195.3	
717830.3	721923.3	745410.3	775116.0	798161.1	812282.1	832090.4	851616.8	
875061.8	895712.8	921316.6	940664.2


*QG is P3S13: Final consumption expenditure of general government (ROW TITLE FOR THE SERIES)
READ QG / BYVAR
34957.0	36239.6	37640.6	39178.1	40397.0	44075.6	48501.6	51093.2	55771.3	57557.4	
61910.5	65128.5	67920.4	70084.2	71852.0	78237.9	84675.6	85217.3	87757.1	90661.0	
92707.4	96919.4	98375.9	100878.3	105580.1	112979.2	117886.7	121880.2	126466.4	
130187.7	133636.9	137914.5	141430.1	145026.8	146603.4	151545.5	157896.8	
161045.6	167575.3	175165.5	180558.2	183609.6	189258.1	194857.4	203486.3	
210079.7	216658.5	223486.8	230247.7	239851.5	244008.9	252311.0	261525.2	
262289.9	266325.3	272790.3	284436.0	298667.0	310352.7	326419.9


*QI1 is P51: Gross fixed capital formation (USUALLY KNOWN AS INVESTMENT IN MACHINERY AND EQUIPMENT AND STRUCTURES)
READ QI1 / BYVAR
37268.2	39382.6	40082.8	42588.3	46577.9	50063.3	54134.8	56894.2	61089.9	64994.9	
67125.9	69288.1	71233.2	73412.1	76545.0	71639.0	74099.4	76982.1	79933.8	87748.3	
90268.5	101813.2	109195.2	98855.0	103877.0	113859.3	120612.0	121078.5	
130147.1	144101.5	145407.2	131623.0	126361.8	134745.8	142552.0	158268.5	
162600.4	173393.3	190277.3	198724.9	214809.4	197746.8	215790.2	242909.6	
264129.2	280226.6	303411.6	317124.9	344911.4	350168.0	356288.6	369114.0	
411696.8	424063.1	416594.5	403414.9	389549.7	388681.1	405668.2	406851.9


*THE OECD DOES NOT ALWAYS PROVIDE A CONSTANT DOLLAR SERIES FOR INVENTORY CHANGE SINCE THERE ARE PROBLEMS IN CONSTRUCTING 
*QUANTITY INDEXES WHEN THE UNDERLYING VALUE AGGREGATE CHANGES SIGN GOING FROM ONE PERIOD TO THE NEXT.
*THE APPROPRIATE WAY TO DEAL WITH INVENTORY CHANGE IS TO DEFLATE BEGINNING AND END OF PERIOD VALUES OF INVENTORY 
*STOCKS AND THEN DIFFERENCE THE RESULTING REAL STOCKS. FOR MORE DETAILS ON THE PROBLEMS ASSOCIATED WITH THE WAY NATIONAL 
*INCOME ACCOUNTANTS MEASURE REAL INVENTORY CHANGE, SEE: W.E. DIEWERT (2005),“On Measuring Inventory Change in Current and 
*Constant Dollars”,Discussion Paper 05-12, Department of Economics, University of British Columbia,Vancouver,B.C.,Canada,V6T 1Z1
*WE DO NOT HAVE ESTIMATES FOR THE VALUE OF REAL INVENTORY STOCKS IN GENERAL SO WE WILL SIMPLY USE THE INVESTMENT GOODS 
*DEFLATOR (TO BE CONSTRUCTED SHORTLY) TO DEFLATE THE VALUE OF INVENTORY CHANGE. 

*QX is P6: Exports of goods and services
READ QX / BYVAR
20274.6	21286.4	24168.8	23630.9	27527.3	27455.4	27855.1	30908.7	32393.3	34505.9	
40171.3	44057.4	47356.6	48310.1	45390.1	49830.1	51777.2	55476.0	56726.5	60750.8	
65044.6	61887.1	63347.7	63677.7	68536.8	79118.4	82111.5	90342.8	97978.1	98993.3	
103617.2	115280.1	126180.3	135530.8	148390.6	154939.8	170371.3	188914.7	
197565.2	201349.7	221266.9	240388.1	239070.3	240261.4	243359.6	251778.2	
259258.1	269996.1	280819.8	287573.4	301083.0	303673.0	317650.8	334497.2	
354409.8	378620.9	404536.8	426801.9	441249.1	456349.8


*QM is P7: Imports of goods and services
READ QM / BYVAR
12851.7	14460.2	12398.0	14546.7	16181.0	19269.2	19799.3	20133.3	22123.7	22887.5	
24795.1	25555.2	23516.9	23814.1	31038.7	31837.5	30275.9	33158.0	31604.6	34154.6	
34188.6	37422.2	41750.9	38226.5	40516.0	47258.4	47147.0	44918.3	49959.2	62390.3	
65894.5	62129.0	64302.5	68542.8	73685.0	85726.9	89744.5	98623.6	108729.4	114288.4	
128057.5	126809.7	128520.3	145481.7	165119.9	185759.3	200869.2	222091.1	
254166.9	245462.6	262986.2	289987.0	322933.3	323818.6	316263.6	319312.3	
318935.0	333934.4	356083.9	359958.7


*Now GENR the implicit prices that correspond to the above data.
*SMPL 11 58 which corresponds to the years 1960-2018.

SMPL 1 60
GENR PC=VC/QC
GENR PG=VG/QG
GENR PI1=VI1/QI1
GENR PX=VX/QX
GENR PM=VM/QM
FORMAT(F8.0,5F11.1)
PRINT YEAR QC QG QI1 QX QM / FORMAT
FORMAT(F8.0,5F11.5)
*NOTE THE CHANGE IN THE FORMAT STATEMENT; WE NEED 5 DECIMAL PLACES FOR THE PRICE SERIES AND ONLY 1 DECIMAL FOR THE VALUE
*AND QUANTITY SERIES
PRINT YEAR PC PG PI1 PX PM / FORMAT

***
*We will use the investment price index to deflate inventory change and then add the deflated value of inventory change
*to real reproducible investment to obtain overall investment. The use of the investment price deflator to deflate
*inventory change is not the correct deflator for inventories. But it is too difficult to get the correct deflator
*and since inventory change is generally small relative to the size of the economy, the errors in using the 
*incorrect deflator will not affect our results to any substantial degree.
GENR PI=PI1
GENR QI2=VI2/PI
GENR QI=QI1+QI2
GENR VI=PI*QI
FORMAT(F8.0,5F10.5,5F11.1)
PRINT YEAR PC PG PI PX PM QC QG QI QX QM / FORMAT
*All prices are 1 in 2010 because it is the OECD's base year
*GRAPH(PC,PG,PI,PX,PM,YEAR)
*GRAPH(QC,QG,QI,QX,QM,YEAR)
*GRAPH(VC,VG,VI,VX,VM,YEAR)
*Graphing the variables show there is no discontinuities
*You see high inflation in the 70s, a dip during the 2008 recession. The quantity data shows periods of fast growth in the 60s, end of the 80s, 
*and beginning of the 2000s.

**Now because I managed to get all the National Accounts data I don't need to use volume indices for 1960-1970.
*I can now 

*Now rescale prices to be 1 in 1959
SMPL 1 60
GENR CON=PC(1)
GENR PC=PC/CON
GENR QC=QC*CON
GENR CON=PG(1)
GENR PG=PG/CON
GENR QG=QG*CON
GENR CON=PI(1)
GENR PI=PI/CON
GENR QI=QI*CON
GENR CON=PX(1)
GENR PX=PX/CON
GENR QX=QX*CON
GENR CON=PM(1)
GENR PM=PM/CON
GENR QM=QM*CON
*Rescale quantities to make them closer to one. This will help when running nonlinear regressions
*for Australia, this means that we are measuring values in billions of dollars instead of millions
GENR CON=1000
GENR VC=VC/CON
GENR QC=QC/CON
GENR VG=VG/CON
GENR QG=QG/CON
GENR VI=VI/CON
GENR QI=QI/CON
GENR VX=VX/CON
GENR QX=QX/CON
GENR VM=VM/CON
GENR QM=QM/CON 
FORMAT(F10.0,10F12.3)
PRINT YEAR VC VG VI VX VM QC QG QI QX QM / FORMAT 

*note the effects of the great recession (which started in 2008 but the effects show up in 2009)
*consumption, government output remained about the same but investment, exports and imports
*dropped substantially in 2009 (and then recovered in 2011). 

*GRAPH(PC,PG,PI,PX,PM,YEAR)

*PG increased by 21-fold and PC by 15-fold. 
*This is explained by the fact that government spends a lot on services which are very labour heavy. 
*Price of labour has increased relative to price of capital across time.
*PI has increased less (8x) for the same reason.
*PX and PM only increased 10x and 6x respectively, because of the extension of world trade.
*PX is also the most volatile price.  Australia's exports are mostly natural resources, which have highly fluctuating prices.

*Calculate the terms of trade
GENR TOFT=PX/PM
FORMAT(F10.0,3F10.5)
*GRAPH(TOFT,PM,PX,YEAR)

*There are three major shocks to TOT in the sample. The first is the oil shock that occurred in 1973-74.
*For countries that have to import all of their oil, we would
*expect the terms of trade to drop during that shock but for Australia, this did not occur since they are an energy exporter.
*The second shock is a dip in TOT in 1985-1986, which occurs at the time of a dropin oil prices and gold prices (two major exports of Australia.
*The third shock is a long increase in TOT between 2004 and 2010. In those years, the prices of oil, gold, and other ores which Australia exports
*(e.g. iron) increased a lot. The TOT decreased somewhat after 2011 due to the oil price crash, but stayed positive as the prices of gold and
*iron did not crash as that of oil did.

**Now let's do input series.
SMPL 1 60
*We download some additional series from the national accounts on the income side of the accounts
GENR VD=0
GENR QD=0
GENR PD=0
*For depreciation, the relevan row is K1MS1: Consumption of fixed capital
SMPL 1 60
READ VD / BYVAR
2865	2999	3053	3251	3546	3998	4186	4561	5014	5344	6402	6753	
7509	8051	9567	10809	12769	14392	16193	17890	20575	23340	26881	30500	
33870	36987	42461	49027	54498	60610	66690	70304	72525	76585	80886	84186	
87507	89667	96519	102358	108496	118320	125444	132101	139907	151172	
163832	178371	191695	206575	216429	226953	237048	250626	268390	
286924	302801	310831	322128	332622

*We will set the price of depreciation equal to the price of investment
SMPL 1 60
GENR PD=PI

*Divide by 1000 to make the units smaller and generate the volume series for depreciation
SMPL 1 60
GENR VD=VD/1000
GENR PD=PI
GENR QD=VD/PD
FORMAT(F8.0,F10.5,2F12.3)
SMPL 1 60
PRINT YEAR PD VD QD / FORMAT

***
*USING THE GEOMETRIC MODEL OF DEPRECIATION, THE CAPITAL STOCK IN PERIOD t K(t) IS EQUAL TO THE STOCK IN PERIOD t-1
*K(t-1) PLUS THE INVESTMENT IN PERIOD t-1 QI(t-1) LESS THE AMOUNT OF DEPRECIATION IN PERIOD t-1 QD(t-1). THUS IF 
*WE KNEW THE STARTING CAPITAL STOCK IN PERIOD 1, WE COULD BUILD UP ALL OF THE CAPITAL STOCKS FOR PERIODS 2 TO 60
*USING THE EQUATION K(t) = K(t-1) + QI(t-1) - QD(t-1). WE WILL USE THIS EQUATION BY MAKING A GUESS AT THE STARTING 
*CAPITAL STOCK. FOR MOST OECD COUNTRIES, THE STARTING CAPITAL STOCK WILL BE AROUND 2 TO 2.5 TIMES THE VALUE OF 
*GDP.WE FIRST CALCULATE GDP USING OUR COMPONENTS OF GDP.
SMPL 1 60
*GENR NOMINAL GDP EQUAL TO VY
GENR VY=VC+VG+VI+VX-VM
*CHANGE THE SIGN OF THE QUANTITY OF IMPORTS (WHICH IS PRESENTLY POSITIVE)
GENR QM=-QM
INDEX PC QC PG QG PI QI PX QX PM QM / CHAIN DIVISIA=PY QDIVISIA=QY
GENR CHECK=VY-PY*QY
FORMAT(F8.0,2F10.5,2F10.3)
PRINT YEAR CHECK PY QY VY / FORMAT
*CHANGE THE SIGN OF QM BACK TO POSITIVE
GENR QM=-QM 
*Check is 0 so our series make sense. So we have our output values to calculate capital stock

*GENR BEGINNING OF THE YEAR CAPITAL STOCK FOR 1960
SMPL 1 61
GENR QK=0
GENR PK=0
GENR VK=0
SMPL 1 1
*WE MAKE A GUESS AT THE STARTING CAPITAL OUTPUT RATIO IN 1960 (OUR GUESS IS 2.5). FOR YOUR COUNTRY, THE GUESS
*SHOULD BE SOMEWHERE BETWEEN 2 AND 3. AFTER WE CALCULATE BALANCING RATES OF RETURN, WE FIND THAT THEY ARE
*VERY HIGH OR VERY LOW, WE CAN REVISIT OUR DECISION TO USE 2.5 IN THE STATEMENT BELOW.
*2.5 LED TO INTEREST RATES TOO HIGH. I AM REPLACING IT WITH 3.
GENR QK=3*QY
SMPL 2 61
GENR QK=LAG(QK)+LAG(QI)-LAG(QD)
*WE WILL SET THE PRICE OF THE BEGINNING OF THE YEAR CAPITAL STOCK TO THE INVESTMENT PRICE OF THE PREVIOUS YEAR
*BUT FOR OBS 1,WE WILL SET THE PRICE OF K AT THE BEGINNING OF 1960 EQUAL TO THE INVESTMENT PRICE OF 1960
SMPL 1 1
GENR PK=PI
SMPL 2 61
GENR PK=LAG(PI)
SMPL 1 61
GENR VK=PK*QK
FORMAT(F8.0,F10.5,8F10.3)
SMPL 1 60
PRINT YEAR PK QK VK VY QY QD VD QI VI / FORMAT
*GENR DEPRECIATION RATES AND REAL AND NOMINAL CAPITAL OUTPUT RATIOS
SMPL 1 60
GENR D=0
GENR KYR=0
GENR VKYR=0
GENR D=QD/QK
*GENR REAL AND NOMINAL CAPITAL OUTPUT RATIOS
GENR KYR=QK/QY
GENR VKYR=VK/VY
FORMAT(F8.0,3F10.5)
PRINT YEAR D KYR VKYR / FORMAT
STAT D KYR VKYR
 *NAME        N    MEAN        ST. DEV      VARIANCE     MINIMUM      MAXIMUM
 *D            60  0.62871E-01 0.65217E-02 0.42532E-04  0.52961E-01  0.72516E-01
 *KYR          60   3.4942     0.40127     0.16102       2.9596       4.3655
 *VKYR         60   2.5515     0.21526     0.46337E-01   2.1922       3.0566

*THE PRICE OF CAPITAL HAS DECLINED RELATIVE TO THE PRICE OF GDP OVER TIME AND SO THE NOMINAL CAPITAL OUTPUT
*RATIO, VKYR, HAS FALLEN RELATIVE TO THE REAL CAPITAL OUTPUT RATIO, KYR.
*THE AVERAGE DEPRECIATION RATE IS 6.2% PER YEAR AND THE RATES VARY BETWEEN 5.3% AND 7.3% WHICH IS PRETTY STABLE.
*FOR MOST COUNTRIES, DEPRECIATION RATES SHOULD INCREASE OVER THE SAMPLE PERIOD DUE TO THE GROWTH OF
*HIGH DEPRECIATION MACHINERY AND EQUIPMENT RELATIVE TO LOW DEPRECIATION STRUCTURES. HOWEVER, FOR AUSTRALIA, THE
*DEPRECIATION RATE HAS A U SHAPE. IT WAS AROUND 5.8% AT THE START (1960S) A DIPPED TO A LOW OF 5.3% IN 1976, AND PROGRESSIVELY INCREASED
*TO REACH 6.9% IN 2018

***
*LET US SEE IF THERE IS A TREND IN THE DEPRECIATION RATES
SMPL 1 60
OLS D T / LIST
*OVERALL,THERE IS A TINY UPWARD TREND IN THE DEPRECIATION RATES; ABOUT .03 PERCENTAGE POINTS PER YEAR.
*HOWEVER, SOME VERY DEFINITE TRENDS CAN BE SEEN IN THE PLOT OF THE RESIDUALS.  
*SEE IF THERE IS A TREND IN THE REAL CAPITAL OUTPUT RATIO
OLS KYR T / LIST
*YES, THERE IS AN UPWARD TREND IN THE REAL CAPITAL OUTPUT RATIO OF ABOUT 2.1 PERCENTAGE POINTS PER YEAR.
*THIS INCREASE IN THE CAPITAL OUTPUT RATIO REFLECTS CAPITAL DEEPENING.

***
*WE TURN OUR ATTENTION TO LABOUR AND OTHER COST COMPONENTS OF GDP
*THE DATA FROM 1959-2018 ARE COPIED FROM OECD.STAT AND ORIGINALLY APPEARED IN THE
*NATIONAL ACCOUNTS OF OECD COUNTRIES: MAIN AGGREGATES, VOLUME 1; PARIS: OECD VARIOUS YEARS.
*VE IS COMPENSATION OF EMPLOYEES (THIS INCLUDES FRINGE BENEFITS).
*THE ROW IN THE TABLE IS:   D1: Compensation of employees; Current prices; Australian Dollars, millions						
SMPL 1 60
GENR VE=0
READ VE / BYVAR
7793	8391	8661	9179	10038	11271	12213	13367	14566	16116	18134	
20826	23270	25962	31766	40982	47344	53397	58516	62867	69990	80897	
93788	103910	110004	120975	133060	144577	158975	177714	199479	
207361	210208	218663	227856	241667	258949	277286	289213	307709	
324662	346524	364905	387306	413486	445138	477521	521717	568055	
591100	614167	669801	715652	739661	765782	788124	813328	833249	
870886	908781
*WE AGAIN DIVIDE BY 1000 TO SWITCH OUR UNITS FROM MILLIONS OF DOLLARS TO BILLIONS OF DOLLARS
GENR VE=VE/1000
***

*NOW WE HAVE TO IMPUTE COMPENSATION FOR THE SELF EMPLOYED AND UNPAID FAMILY WORKERS WHICH IS NOT INCLUDED IN EMPLOYEE COMPENSATION. 
*THE DATA FROM EMPLOYER SURVEYS ONLY COVERS 1985-2018 (FOR EMPLOYEES AND SELF-EMPLOYED) AND 1978-2018 (FOR ALL CIVIL EMPLOYMENT)
SMPL 1 60
GENR QCE=0
GENR QE=0
GENR QSE=0

*FIRST WE WILL INPUT SERIES FOR ALL CIVIL EMPLOYMENT. THIS INCLUDES BOTH EMPLOYEES AND SELF-EMPLOYED. WE ONLY HAVE DATA FOR THIS FROM 1978 TO 1979.
*THE OECD TABLE IS: Dataset: 3. Population and employment by main activity; country=Australia; Measure=Persons
*ROW TITLE IS: ETONC: Total employment, national concept
SMPL 20 59
READ QCE / BYVAR
6053.997333	6193.108632	6361.200275	6439.938546	6328.899133	6387.558215	6579.294144	6859.951081	7044.345081	7256.271374	7548.626808	
7831.957228	7703.716449	7631.400055	7620.506789	7857.908786	8165.2016	8297.125431	8330.18644	8482.540018	8606.520123	8864.999781	
9004.629114	9134.673081	9380.077847	9533.473324	9850.755076	10082.05588	10412.00734	10690.30636	10786.79871	10957.42661	11196.18107	
11393.15298	11491.66477	11553.34876	11781.44093	11999.94942	12256.63391	12571.67845

*THE DATA FOR 2018-2019 (OBSERVATION 2018 HERE) IS NOT OUT YET ON OECD. SO WE HAVE TO GET IT FROM ABS WEBSITE. THE SERIES THERE MATCHES THE ONE
*ABOVE FOR OECD FROM 1978 TO 1990 AND DIVERGES SLIGHTLY AFTERWARDS. THE DIFFERENCE IS VERY MINIMAL. WE CAN USE THE 2018-2019 GROWTH RATE FROM
*ABS TO EXTRAPOLATE THE OECD SERIES.
*FROM THE ABS WEBSITE WE DOWNLOAD THIS MONTHLY DATA (JULY 2017-JUIN 2019) AND AGGREGATE IT TO ANNUAL SERIES FOR 2017-2018 AND 2018-2019.
*THE TABLE TITLE IS Table 1. Labour force status by Sex, Australia - Trend, Seasonally adjusted and Original 
*THE COLUMN TITLE IS Employed total ;  Persons ;000; Original; STOCK; Month; Feb-1978 TO May-2019
SMPL 1 24
READ  QCENEW / BYVAR
12260.50268	12244.92213	12333.43397	12350.84944	12453.79483	12567.96388	12305.08399	12528.05935	12494.49825	12525.74011	12572.24455	
12607.72335	12574.45211	12546.98762	12615.01883	12654.65871	12740.71859	12838.76388	12604.47717	12806.39442	12791.92096	12842.91279	
12935.7785 12950
SMPL 1 12

STAT QCENEW / MEAN=QCE17
SMPL 13 24
STAT QCENEW / MEAN=QCE18
SMPL 1 1
GENR CON=QCE18/QCE17
FORMAT(2F10.1,F10.5)
PRINT QCE17 QCE18 CON / FORMAT
*NOW WE CAN USE CON TO GENR AN ESTIMATE FOR QCE FOR 2018
SMPL 60 60
GENR QCE=QCE(59)*CON(1)

*NOW INPUT THE SERIES FOR EMPLOYEES
*THE OECD TABLE IS: Dataset: 3. Population and employment by main activity; country=Australia; Measure=Persons
*ROW TITLE IS: ETONC: Total employment, national concept;   EEMNC: Employees, national concept
*EMPLOYEES ARE PAID WORKERS. THIS CATEGORY OF WORKER SHOULD CORRESPOND TO THE COMPENSATION OF EMPLOYEE SERIES THAT WE TABLED ABOVE.
*START FROM 1985
SMPL 27 59
READ QE / BYVAR
5824.244333	5966.230977	6165.525713	6428.548321	6717.188709	6519.744802	6439.715379	
6421.009813	6645.862538	6930.945044	7039.5997	7115.344387	7255.007597	7407.99437	
7678.241646	7791.379162	7880.783123	8146.185565	8324.718673	8604.098237	8859.638783	
9213.435322	9474.890096	9575.372616	9697.059667	9945.878113	10205.1855	10320.61027	
10363.42338	10557.91481	10767.59121	11008.73792	11361.31916
*THERE ARE TWO OTHER CLASSES OF WORKERS WHO ARE NOT EMPLOYEES: (1) EMPLOYERS AND PERSONS WORKING ON OWN ACCOUNT
*(THESE WORKERS ARE THE SELF EMPLOYED) (SE) AND (2) UNPAID FAMILY WORKERS (UP) 
*IN THE ABOVE TABLE, 3. Population and employment by main activity, THE SE AND UP WORKERS ARE AGGREGATED INTO 
*ONE CATEGORY, WHICH IS CALLED THE SELF EMPLOYED. THE HARD COPY DATA USUALLY DISTINGUISHES THESE TWO CLASSES
*OF WORKER SO THAT THERE ARE 3 CLASSES OF WORKER IN ALL. WE WILL JUST WORK WITH EMPLOYEES AND THE SELF EMPLOYED
*WHICH IS ACTUALLY AN AGGREGATE OF THE SELF EMPLOYED AND UNPAID FAMILY WORKERS. WE READ IN THE DATA FOR THIS 
*AGGREGATE CATEGORY.
*THE OECD TABLE IS: Dataset: 3. Population and employment by main activity; country=Australia; Measure=Persons
*ROW TITLE IS: ETONC: Total employment, national concept;  ESENC: Self-employed, national concept
*READ IN THE DATA FOR 1985-2018
READ QSE / BYVAR
1035.706748	1078.114104	1090.745661	1120.078487	1114.768518	1183.971647	1191.684676	1199.496977	
1212.046248	1234.256557	1257.52573	1214.842053	1227.532422	1198.525753	1186.758135	1213.249952	
1253.889959	1233.892282	1208.754651	1246.656839	1222.417092	1198.572022	1215.416261	1211.426098	
1260.366942	1250.302954	1187.96748	1171.054495	1189.925385	1223.526123	1232.358211	1247.895989	
1210.359293

*WE CAN GET THE OBSERVATION FOR 2018-2019 FOR QE AND QSE USING THE INFORMATION FOR QCE FROM ABS WEBSITE (SEE ABOVE)
*LOOK AT THE RATIO OF QSE TO QCE
SMPL 27 60 
GENR RATIO=QSE/QCE
FORMAT(F8.0,3F10.1,F10.5)
PRINT YEAR QE QSE QCE RATIO / FORMAT
*THE SERIES LOOK OK
*GENR AN ESTIMATE FOR QSE FOR 2018 USING RATIO FOR 2017
SMPL 60 60
GENR QSE=QCE*RATIO(59)
GENR QE=QCE*(1-RATIO(59))
SMPL 27 60
FORMAT(F8.0,3F10.1)
PRINT YEAR QE QSE QCE  
*THE ABOVE SERIES LOOK OK

*GRAPH(QE,QSE,YEAR)
*LOOKS GOOD

SMPL 1 60
GENR POP=0
*WE ALSO READ IN A TOTAL POPULATION FROM THE SAME TABLE. THIS INFORMATION WILL BE USED WHEN ESTIMATING CONSUMER
*MODELS BUT IT CAN BE USEFUL IN EXTRAPOLATING INCOMPLETE LABOUR SERIES (USING RATES OF CHANGE IN POPULATION)
*THE OECD TABLE IS: Dataset: 3. Population and employment by main activity; country=Australia; Measure=Persons
*ROW TITLE IS: ETONC: POPNC: Total population, national concept	
*READ IN THE DATA FOR 1959-2018
SMPL 1 59
READ POP / BYVAR
10160.968	10391.92	10642.654	10846.059	11055.482	11280.429	11505.408	11704.843	11912.253	
12145.582	12407.217	12663.469	13198.38	13409.288	13614.344	13831.978	13968.881	14110.107	
14281.533	14430.83	14602.481	14807.37	14923.3	15184.2	15393.5	15579.4	15788.3	16018.4	16263.9	
16532.2	16814.4	17284	17478.6	17634.8	17805.5	18004.9	18224.8	18423	18607.6	18812.3	19028.8	
19274.7	19495.2	19720.7	19932.7	20176.8	20451	20827.6	21249.2	21691.7	22031.8	22340	22733.5	
23128.1	23475.7	23816	24190.9	24601.9	24992.9	
*FOR 2018-2019, I USE THE POPULATION ESTIMATE FROM THE UN WEBSITE
SMPL 60 60
READ POP / BYVAR 
25417.67508
*GRAPH(POP,YEAR)
*THERE SEEMS TO BE DISCONTINUITIES IN 1971 AND 1990. THE DISCONTINUITY IN 1971 IS LIKELY DUE TO A CHANGE IN METHODOLOGY
*SEE THE EXPLANATION ON THE ABS WEBSITE (https://www.abs.gov.au/AUSSTATS/abs@.nsf/Lookup/3101.0Explanatory%20Notes1Sep%202018?OpenDocument)
*I'M NOT SURE WHAT ACCOUNTS FOR THE JUMP IN 1990. 

***
*WE NEED TO FILL IN THE DATA FOR QCE FOR THE YEARS 1959-1978 AND THE DATA FOR QE AND QSE FOR THE YEARS 1959-1984.
*THE LFS DATA HAS RELEVANT NUMBERS FROM 1964, AS WELL AS A CIVIL EMPLOYMENT SERIES WITH OBSERVATIONS FOR 1959-1961 WHICH I WILL USE TO 
*EXTRAPOLATE THE SERIES

*FIRST WE WILL USE AFLS DATA TO FILL QCE FOR 1959-1978
*Dataset: Employment by activities and status (ALFS)
*NOTE: ALFS STANDS FOR DATA TAKEN FROM COUNTY LABOUR FORCE SURVEYS
*WHICH ARE HOUSEHOLD SURVEYS SO THAT STATISTICAL AGENCIES CAN DETERMINE SE AND UP. HOWEVER, THE SAMPLES ARE
*USUALLY SOMEWHAT SMALL AND RESPONSES ARE NOT ENTIRELY ACCURATE SO THE INFORMATION IS NOT AS GOOD AS THE QE
*SERIES WHICH USES EMPLOYEE INFORMATION FROM FIRMS AS WELL AS LABOUR FORCE SURVEY INFORMATION.
*OECD.STAT: Employment by activities and status (ALFS); SEX = all persons; country = Australia; Frequency = Annual
*ROW TITLE: Civil Employment all status, all activities; Persons, Thousands
*THE DATA START IN 1964 AND END IN 2017
SMPL 6 59
READ QCEAFLS / BYVAR
4488.0 	4629.0 	4823.9 	4933.0 	5055.6 	5183.0 	5395.6 	5515.7 	5609.9 	5783.0 	5855.2 	5841.3 	5897.8 	5995.4 	6015.8 	6098.4 	
6276.0 	6399.0 	6404.9 	6288.9 	6474.3 	6697.4 	6974.5 	7128.7 	7398.2 	7719.8 	7858.8 	7670.5 	7634.4 	7653.4 	7899.3 	8188.8 	
8298.9 	8366.7 	8517.4 	8672.6 	8901.7 	9015.0 	9190.3 	9396.5 	9558.4 	9880.6 	10124.2 	10434.3 	10731.2 	
10805.6 	11022.2 	11180.0 	11324.0 	11443.0 	11561.0 	11808.0 	11999.0 	
12276.0 
*WE CAN EXTRAPOLATE QCE BACKWARDS USING THIS DATA EVEN THOUGH IT DOES NOT AGREE PERFECTLY WITH QCE.
SMPL 6 19
GENR QCE=QCEAFLS*QCE(20)/QCEAFLS(20)

*NOW WE CAN INPUT DATA FOR EMPLOYEES FROM ALFS
*OECD.STAT: Employment by activities and status (ALFS); SEX = all persons; country = Australia; Frequency = Annual
*ROW TITLE: Employees in all activities; Persons, Thousands
*THE SERIES RUNS FROM 1964-2017
SMPL 6 59
READ QEALFS / BYVAR
3752	3889	4077	4205	4330	4461	4649	4760	4835	4997	5026	5001	5002	5042	5082.9	
5125.9	5269.5	5399.5	5402.6	5305.6	5460.9	5690.503	5906.334	6058.577	6283.452	
6601.332	6724.502	6524.553	6486.319	6478.231	6723.878	6996.648	7117.872	7180.429	
7352.219	7487.159	7697.837	7810.604	7949.904	8162.858	8335.686	8635.974	8900.288	
9214.759	9500.286	9560.695	9752.813	9925	10134	10269	10378	10578	10772	11033
*THE SAME TABLE HAS AN ALTERNATIVE EMPLOYEE SERIES (USING A DIFFERENT CLASSIFICATION SCHEME). THE ROW TITLE IS:
*Employees in all activities (ISIC rev.2, 1-10); Persons, Thousands
*WE READ IN THIS SERIES FOR THE YEARS 1959-1964
SMPL 1 6
READ QEOLD / BYVAR
3005.0 	3127.0 	3133.0    0   0   3752.0
*NOTE: WE ENTERED 0's FOR 1962 AND 1963 BECAUSE THE DATA WERE MISSING FOR THESE TWO YEARS
*FILL IN THE MISSING DATA BY LINEAR INTERPOLATION
SMPL 1 1
GENR CON=QEOLD(6)-QEOLD(3)
SMPL 4 5
GENR QEOLD=LAG(QEOLD)+(1/3)*CON(1)
*PRINT OUT THE RESULTING SERIES
SMPL 1 6
FORMAT(F8.0,2F10.2)
PRINT YEAR QEOLD QEALFS / FORMAT
*SINCE QEALFS INCIDES WITH QEOLD FOR 1964, WE CAN EXTEND QEALFS TO COVER SMPL 1 5 AS FOLLOWS:
SMPL 1 5
GENR QEALFS=QEOLD
*WE CAN EXTRAPOLATE QE BACKWARDS USING THIS DATA EVEN THOUGH IT DOES NOT AGREE PERFECTLY WITH QE.
SMPL 1 26
GENR QE=QEALFS*QE(27)/QEALFS(27)
*WE WILL USE THE RATE OF GROWTH OF QE FOR THE YEARS 1959-1963 TO EXTEND THE CIVILIAN EMPLOYMENT SERIES BACKWARDS
SMPL 1 5
GENR QCE=QE*QCE(6)/QE(6)
*NOW WE CAN DEFINE A CONSISTENT SELF EMPLOYMENT (PLUS UNPAID FAMILY WORKERS) BY SUBTRACTING QE FROM QCE
SMPL 1 26
GENR QSE=QCE-QE
SMPL 1 60
FORMAT(F8.0,3F10.2)
PRINT YEAR QCE QE QSE / FORMAT
*THESE SERIES LOOK VERY GOOD. I MANAGE TO USE THE HIGH QUALITY INFORMATION FROM EMPLOYER SURVEYS AND FILL IN THE BLANKS USING AFLS DATA.

*WE NEED TO CONVERT THE NUMBER OF WORKERS INTO THE NUMBER OF HOURS WORKED. WE GO TO ANOTHER OECD.STAT TABLE WHICH HAS 
*AVERAGE HOURS WORKED BY EMPLOYEES FOR THE YEARS 1979-2017 FOR AUSTRALIA
SMPL 1 60
GENR HOURS=0
*OECD.STAT TABLE: Dataset: Average annual hours actually worked per worker; Total employment; frequency=annual
*unit=hours; ROW TITLE: Australia
SMPL 21 59
READ HOURS /BYVAR
1832.3	1830.9	1816.8	1792.5	1779.1	1801	1793.3	1797.9	1795.4	1793.4	1806.4	
1788.3	1773.6	1772.4	1790.9	1799.8	1799	1792.28	1790.78	1776.694	1781.5	1779.7	
1746.4	1741.9	1734.9	1733.64	1732	1721.7	1722.6	1720.8	1692.1	1699.7	1699.2	
1691.9	1689.599976	1682.699951	1682.900024	1672.199951	1675.900024
*WE ASSUME THE HOURS IN 2018 ARE EQUAL TO THE HOURS FOR 2017
SMPL 60 60
GENR HOURS=HOURS(59)

*WE CAN GET HOURS DATA FOR AUSTRALIA BACK TO 1970 USING THE OECD.STAT TABLE FOR 2008 THAT PROF DIEWERT SENT ME.
SMPL 12 49 
READ HOURSOLD / BYVAR
1830	1827	1823	1819	1816	1812	1808	1805	1802.3	1822.7	1798.1	1798.1	1787.6	1774.1	1789.6	1785.6 
1793.1	1802.1	1810.6	1804.9	1795.3	1775.1	1785.5	1799.4	1806.7	1802.5	1799	1795.2	1788.5	1786.9	1785.3	1754.6
1740.3	1737.4	1747	1731.5	1723.3	1721.7
*COMPARE THE TWO SERIES
FORMAT(F8.0,2F10.1)
PRINT YEAR HOURSOLD HOURS / FORMAT
*THE SERIES ARE REASONABLY CONSISTENT. WE WILL USE HOURSOLD TO EXTEND HOURS BACK TO 1970
SMPL 12 20
GENR HOURS=HOURSOLD*HOURS(21)/HOURSOLD(21)

*WE ARE MISSING HOURS DATA FOR 1959-1970. I WILL USE THE GROWTH RATE IN HOURS IN CANADA AS A PROXY FOR THE GROWTH RATE OF HOURS IN AUSTRALIA
*IN THAT PERIOD. THE TWO COUNTRIES ARE SIMILAR, AND THEIR SERIES FOR HOURS WORKED FOLLOW EACH OTHER VERY CLOSELY AFTER 1979.
*FIRST INPUT CANADA'S HOUR DATA FROM 1961-2017
SMPL 3 59
READ HOURSCA /BYVAR
2059 	2058 	2041 	2033 	2016 	2001 	1993 	1958 	1943 	1925 	1912 	1904 	1898 	1891 	1872 	
1862 	1834 	1836 	1841 	1827 	1812 	1789 	1779 	1789 	1795 	1797 	1807 	1808 	1802 	1797 	
1775 	1772 	1769 	1778 	1775 	1790 	1785 	1782 	1782 	1779 	1771 	1754 	1746 	1759 	1747 	
1745 	1741 	1735 	1701 	1703 	1700 	1713 	1711 	1706 	1711 	1706 	1695 

*SMPL 21 59
*GRAPH(HOURS,HOURSCA,YEAR)
*THEY FOLLOW EACH OTHER VERY CLOSELY

SMPL 3 59
GENR HOURSCAG=HOURSCA/LAG(HOURSCA)-1

DO #=11,3,-1
  SMPL # #
  GENR HOURS=HOURS(#+1)/(1+HOURSCAG(#+1))
ENDO

*ASSUME HOURS FOR 1959 AND 1960 ARE THE SAME AS HOURS FROM 1961
SMPL 1 2
GENR HOURS=HOURS(3)



SMPL 1 60
FORMAT(F8.0,F10.2)
PRINT YEAR HOURS / FORMAT

*GENERATE VARIABLES FOR AGGREGATE HOURS WORKED FOR EACH TYPE OF WORKER
SMPL 1 60
GENR QE=HOURS*QE
GENR QSE=HOURS*QSE

*WE CAN CONSTRUCT AN HOURLY WAGE RATE SERIES BY DIVIDING EMPLOYEE COMPENSATION BY QE, TOTAL EMPLOYEE HOURS WORKED.
*SINCE WE ARE MEASURING VE IN BILLIONS OF DOLLARS, WE NEED TO MEASURE QE AND QSE IN TERMS OF BILLIONS OF HOURS
*THUS MAKE THE FOLLOWING CHANGES IN THE UNITS OF MEASUREMENT FOR QE AND QSE:
GENR QE=QE/1000000
GENR QSE=QSE/1000000
GENR WE=VE/QE
FORMAT(F8.0,F10.2)
PRINT YEAR WE / FORMAT
*THESE WAGE RATES LOOK VERY REASONABLE.
*WE HAVE TO DETERMINE AN HOURLY WAGE RATE FOR SELF EMPLOYED AND UNPAID FAMILY WORKER HOURS.
*WE MAKE THE SOMEWHAT ARBITRARY ASSUMPTION THAT THESE WORKERS EARN 2/3 OF THE WAGE OF AN EMPLOYEE.
GENR WSE=(2/3)*WE
*NOW MAKE AN OVERALL WAGE INDEX AND AN OVERALL QUANTITY INDEX FOR LABOUR INPUT USING THE DIVISIA INDEX
*WHICH IS THE CHAINED TORNQVIST THEIL INDEX
INDEX WE QE WSE QSE / CHAIN DIVISIA=PL QDIVISIA=QL
GENR VL=PL*QL
*GENR LABOUR'S SHARE OF GDP
GENR SL=VL/VY
FORMAT(F8.0,3F10.5,4F10.3)
PRINT YEAR SL PY PL QY VY QL VL / FORMAT
*LABOUR'S SHARE HAS FALLEN FROM 0.53 TO 0.50 OVER THE SAMPLE PERIOD; NOT A HUGE FALL BUT A FALL. IT HAS AN INVERTED U-SHAPE WITH A PEAK IN THE 1970S

***NOW DO TAXES
*DEFINITIONS: T1000 = Taxes on income, profits and capital gains THIS IS BROKEN DOWN INTO THE FOLLOWING COMPONENTS:		
*1100 = Taxes on income, profits and capital gains of individuals WE ASSUME THAT THESE TAXES FALL ON LABOUR INPUT		
*1200 = Taxes on income, profits and capital gains of corporates  WE ASSUME THAT THESE TAXES FALL ON CAPITAL INPUT		
*1300 Unallocable between 1100 and 1200                           WE ASSUME THAT THESE TAXES FALL ON CAPITAL INPUT		
*THE ABOVE ALLOCATIONS ARE ONLY APPROXIMATELY CORRECT
*THUS T1000 - T1100 = TAXES ON CAPITAL INPUT
SMPL 1 60
GENR T1000=0
GENR T1100=0
GENR T2000=0
*THE T2000 TAXES ARE SOCIAL SECURITY CONTRIBUTIONS (TAX ON LABOUR) 2000 Social security contributions
GENR T3000=0
*THESE ARE TAXES ON PAYROLLS AND THE WORKFORCE (TAX ON LABOUR) 3000 Taxes on payroll and workforce
GENR T4000=0
*THESE ARE TAXES ON PROPERTY (TAX ON CAPITAL) 4000 Taxes on property
GENR T5000=0
*THESE ARE TAXES ON GOODS AND SERVICES (TAXES ON C+I) 5000 Taxes on goods and services
GENR T5123=0
*THESE ARE CUSTOMS AND IMPORT DUTIES (TAXES ON IMPORTS) 5123 Customs and import duties
GENR T5124=0
*THESE ARE TAXES ON EXPORTS 5124 Taxes on exports
GENR T6000=0
*THESE ARE OTHER TAXES (WE WILL REGARD THESE AS TAXES ON CAPITAL) 6000 Other taxes

*I ADD TAXES TAKEN FROM OECD.STAT FOR 1964-1979. I ALSO HAVE TAX DATA FOR 1960 FROM PAGE 185 
*OF THE HARD COPY OECD PUBLICATION: REVENUE STATISTICS 1965-1990. I ALSO HAVE 1960 OBSERVATION FOR IMPORT AND EXPORT TAXES FROM TABLE SENT BY
*PROF DIEWERT. I INPUT THE OTHER OBSERVATIONS AS 0 FOR NOW.
SMPL 1 58
READ T1000 / BYVAR
0	1612	0	0	0	0	2547	2728	3035	3417	4052	4619	5300	5718	7518	10156	
11831	13968	15336	15948	18572	22383	26457	28075	29650	35335	39435	45432	51368	58540	
64255	66542	63059	64242	67099	73894	82615	90435	95367	107594	118227	124427	122668	
134042	145290	162518	175695	187336	206134	198659	184363	202184	227861	238829	
246073	255966	262357	278764
READ T1100 / BYVAR
0	1037	0	0	0	0	1729	1921	2175	2377	2855	3175	3765	4084	5485	7709	9213	11047	
12118	12791	15033	17532	21205	22943	24671	29256	32673	38000	41810	47432	49881	50062	46830	
47528	51282	54635	60602	66278	70782	81052	87496	80991	89864	94368	102524	112060	118239	
119073	127587	127294	124649	138163	152884	162410	169972	183023	191747	198534
READ T2000 / BYVAR
0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	
0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0
READ T3000 / BYVAR
0	131	0	0	0	0	157	165	180	199	221	234	406	469	695	1040	1226	1385	1488	1582	1758	
1988	2467	2724	2815	3110	3420	4239	4936	5741	6413	7115	7305	7224	7523	
9422	10185	10841	11277	8537	9051	9624	9828	10411	11219	12295	13464	14710	16407	
17304	17304	18492	20307	21432	22210	22776	23354	23799
READ T4000 / BYVAR
0	404	0	0	0	0	576	638	716	809	902	934	986	1156	1336	1483	1869	2151	2288	2444	
2720	3105	3370	3677	4412	5055	5657	6580	8491	10085	9991	10427	11012	11593	
12732	12635	13130	14437	15505	16428	18133	18825	19204	21417	24393	24266	25680	29320	
31156	27929	31928	33526	33612	36039	40963	45293	49641	52585
READ T5000 / BYVAR
0	1258	0	0	0	0	1744	1858	2029	2236	2456	2719	3028	3307	4011	4852	6194	
7047	7559	9114	10757	12402	14038	16326	18834	21603	23727	25279	27975	29636	32477	
32416	31278	32816	36556	40090	43299	44897	46433	49960	51646	61876	66483	72817	77088	
80131	83839	87985	93848	94168	98632	103756	106403	117106	124096	119014	127998	131930
READ T5123 / BYVAR
0	202	0	0	0	0	271	275	312	346	414	466	469	513	604	841	932	1152	1132	1363	1538	1799	
2060	2036	2329	2927	3282	3237	3632	3752	3954	3319	3299	3331	3226	3474	
3124	3289	3637	3639	3799	4606	5214	5573	5622	5548	4988	5644	6070	6276	
5748	5828	7105	8172	9280	10884	14046	14196
READ T5124 / BYVAR
0	1	0	0	0	0	1	1	1	1	1	1	1	1	12	42	116	182	101	155	92	117	99	69	69	69	76	
77	79	78	72	57	51	5	4	6	5	6	6	4	0	0	0	7	9	12	14	14	10	13	14	11	12	
9	10	12	11	11
READ T6000 / BYVAR
0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	
0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0	0
*CONVERT FROM MILLIONS TO BILLIONS
GENR CON=1000
GENR T1000=T1000/CON
GENR T1100=T1100/CON
GENR T2000=T2000/CON
GENR T3000=T3000/CON
GENR T4000=T4000/CON
GENR T5000=T5000/CON
GENR T5123=T5123/CON
GENR T5124=T5124/CON
GENR T6000=T6000/CON
FORMAT(F8.0,9F9.3)
PRINT YEAR T1000 T1100 T2000 T3000 T4000 T5000 T5123 T5124 T6000 / FORMAT
*THERE ARE NO UNEXPLAINED DISCONTINUITIES IN THE ABOVE TAX SERIES SO WE ARE OK.
*THE IMPORT TAX SERIES IS DISCONTINUOUS, BUT ALL THE DISCONTINUITIES CORRESPOND TO MAJOR TAX CHANGES.

***
*NOW WE CONSTRUCT ESTIMATES OF TOTAL TAXES ON C (TC), ON X (TX), ON M (TM), ON L (TL) AND ON K (TK)
*WE ASSUME THAT NO TAXES FALL ON G AND I.THIS IS NOT QUITE CORRECT BUT THE ERROR HERE IS SMALL COMPARED TO OTHER ERRORS.
SMPL 1 60
GENR TC=T5000-T5123-T5124
GENR TX=T5124
GENR TM=T5123
GENR TL=T1100+T2000+T3000
*WE ARE ASSUMING THAT MOST INCOME TAXES T1100 FALL ON LABOUR; THIS IS ONLY A ROUGH APPROXIMATION.PAYROLL TAXES
*T3000 ARE OBVIOUSLY TAXES ON LABOUR INPUT.IT IS SOMEWHAT DEBATABLE WHETHER SOCIAL SECURITY PAYMENTS (WHICH
*ARE LEVIED ON LABOUR PAYMENTS) ARE REALLY A TAX BUT FOR OUR PURPOSES, WE WILL TREAT IT AS A TAX ON LABOUR. 
GENR TK=T1000-T1100+T4000+T6000
*TK ARE CAPITAL TAXES THAT FALL ON THE BEFORE TAX RATE OF RETURN EARNED BY THE PRODUCTION SECTOR PLUS PROPERTY TAXES
*RECALL THAT T1000 = Taxes on income, profits and capital gains, SO THIS CATEGORY FALLS ON LABOUR AND CAPITAL.
*WE HAVE SOMEWHAT ARBITRARILY ASSUMED THAT PERSONAL INCOME TAXES, T11OO, ARE THE PORTION OF T1000 THAT CAN BE
*ATTRIBUTED TO LABOUR SO T1000 - T1100 IS ATTRIBUTED TO CAPITAL. THE TAX CATEGORY T4000 ARE TAXES THAT FALL
*ON PROPERTY AND WEALTH. 
FORMAT(F8.0,5F12.3)
PRINT YEAR TC TX TM TL TK / FORMAT
*IT CAN BE SEEN THAT AT THE END OF THE SAMPLE PERIOD, TAXES ON LABOUR TL ARE THE BIGGEST COMPONENT OF GOVERNMENT 
*TAX REVENUE, FOLLOWED BY TAXES ON PROFITS AND WEALTH TK, FOLLOWED BY CONSUMPTION TAXES TC, FOLLOWED BY PROPERTY TAXES TP.
*IMPORT TAXES ARE VERY LOW AND EXPORT TAXES ARE NEGLIGIBLE.
*IN 1960, TC WERE MORE IMPORTANT THAN TK.
*NOTE ALSO THE STEADY DECLINE IN THE IMPORTANCE OF IMPORT TARIFFS.

*WE NEED TO FILL IN THE BLANKS IN THE LAST TABLE.
*WE FIRST CONVERT THE ABOVE TAX LEVELS INTO TAX RATES
GENR TRC=TC/VC
GENR TRX=TX/VX
GENR TRM=TM/VM
GENR TRL=TL/VL
GENR TRK=TK/VK
*NOTE THAT TRK IS AN ASSET TAX RATE FOR CAPITAL; I.E, CAPITAL TAXES PAID AS A FRACTION OF THE VALUE OF CAPITAL.
FORMAT(F8.0,5F10.5)
PRINT YEAR TRC TRX TRM TRL TRK / FORMAT
*WE CAN INTERPOLATE THE TAX RATES FOR TRC TRM TRI TRL AND TRK FOR THE MISSING YEARS 1960-1964 AND THEN CONSTRUCT
*ESTIMATES FOR THE CORRESPONDING TAX REVENUES FOR THE MISSING YEARS.  WE NOW DO THIS. TAX RATES GENERALLY
*CHANGE SLOWLY OVER TIME. 
*NOTE: IF YOU CANNOT FIND TAX INFORMATION FOR YOUR COUNTRY FOR 1960, THE INTERPOLATION OF TAX RATES THAT IS
*DONE BELOW IS SIMPLY REPLACED BY STATEMENTS THAT SET THE TAX RATES FOR 1960-1964 EQUAL TO THE CORRESPONDING
*TAX RATE FOR 1965. 
SMPL 3 3
GENR TRC=.8*TRC(2)+.2*TRC(7)
GENR TRX=.8*TRX(2)+.2*TRX(7)
GENR TRM=.8*TRM(2)+.2*TRM(7)
GENR TRL=.8*TRL(2)+.2*TRL(7)
GENR TRK=.8*TRK(2)+.2*TRK(7)
SMPL 4 4
GENR TRC=.6*TRC(2)+.4*TRC(7)
GENR TRX=.6*TRX(2)+.4*TRX(7)
GENR TRM=.6*TRM(2)+.4*TRM(7)
GENR TRL=.6*TRL(2)+.4*TRL(7)
GENR TRK=.6*TRK(2)+.4*TRK(7)
SMPL 5 5
GENR TRC=.4*TRC(2)+.6*TRC(7)
GENR TRX=.4*TRX(2)+.6*TRX(7)
GENR TRM=.4*TRM(2)+.6*TRM(7)
GENR TRL=.4*TRL(2)+.6*TRL(7)
GENR TRK=.4*TRK(2)+.6*TRK(7)
SMPL 6 6 
GENR TRC=.2*TRC(2)+.8*TRC(7)
GENR TRX=.2*TRX(2)+.8*TRX(7)
GENR TRM=.2*TRM(2)+.8*TRM(7)
GENR TRL=.2*TRL(2)+.8*TRL(7)
GENR TRK=.2*TRK(2)+.8*TRK(7)

*SET THE TAX RATES FOR 2017 AND 2018 EQUAL TO THE CORRESPONDING TAX RATES FOR 2016
SMPL 59 60
GENR TRC=TRC(58)
GENR TRX=TRX(58)
GENR TRM=TRM(58)
GENR TRK=TRK(58)
GENR TRL=TRL(58)
*SET THE TAX RATES FOR 1959 EQUAL TO THE CORRESPONDING TAX RATES FOR 1960
SMPL 1 1
GENR TRC=TRC(2)
GENR TRX=TRX(2)
GENR TRM=TRM(2)
GENR TRK=TRK(2)
GENR TRL=TRL(2)
*GENR THE CORRESPONDING IMPUTED TAX REVENUES
SMPL 1 60
GENR TC=TRC*VC
GENR TM=TRM*VM
GENR TX=TRX*VX
GENR TL=TRL*VL
GENR TK=TRK*VK 
GENR TTOT=TC+TX+TM+TL+TK
FORMAT(F9.0,6F11.3)
PRINT YEAR TC TX TM TL TK TTOT / FORMAT
FORMAT(F8.0,5F15.5)
PRINT YEAR TRC TRX TRM TRL TRK / FORMAT
STAT TRC TRX TRM TRL TRK 
*AS OPPOSED TO CANADA THERE IS NO SHIFT FROM TAXING CAPITAL TO TAXING LABOUR.
*NOTE ALSO THAT THE IMPORT TAX RATE HAS BEEN STEADILY FALLING.
*WE ALSO NEED TO DEAL WITH SUBSIDIES. THERE ARE TWO TYPES OF SUBSIDY: SUBSIDIES THAT ARE PROPORTIONAL TO OUTPUTS
*PRODUCED BY THE PRODUCTION SECTOR AND SUBSIDIES THAT SIMPLY GIVE LUMP SUM TRANSFERS TO PRODUCERS. THE SECOND 
*TYPE OF SUBSIDY ACTS AS A NEGATIVE TAX ON CAPITAL EMPLOYED IN THE PRODUCTION SECTOR; THE FIRST TYPE OF SUBSIDY
*ACTS AS AN INCREASE IN THE SELLING PRICE OF THE SUBSIDIZED OUTPUTS PRODUCED BY THE SUBSIDIZED INDUSTRIES.
*WE WILL MAKE AN ADJUSTMENT TO SELLING PRICES DUE TO THE FIRST TYPE OF SUBSIDY.

*ABS DOES NOT HAVE DATA ON SUBSIDIES ON PRODUCTS. BUT IT DOES HAVE DATA ON TAXES ON PRODUCTS AND IMPORTS NET OF SUBSIDIES AND DATA ON
*TAXES ON PRODUCTS AND IMPORTS. TAKING THE DIFFERENCE SHOULD GIVE US A GOOD MEASURE OF SUBSIDIES. THE DATA WAS FOUND ON
*THE ABS WEBSITE, IN THE NATIONAL INCOME (CURRENT PRICES) DATASET AND TAXES DATASET. SINCE IMPORT SUBSIDIES ARE LIKELY NEGLIGIBLE RELATIVE
*TO SUBSIDIES TO DOMESTIC PRODUCTION, WE CAN USE IT WITHOUT PROBLEM.

*SMPL 1 60
GENR VS=0
GENR VTAXPI=0
GENR VTAXPIS =0

*FIRST READ TAXES ON PRODUCTS AND IMPORTS FROM ABS TAX DATASET
SMPL 1 59
READ VTAXPI / BYVAR
1553	1648	1646	1791	1933	2148	2336	2508	2762	3070	3389	3691	4222	4745	5887	
7249	9264	10584	11393	13289	15579	17854	20519	23720	27259	31335	34598	37530	42630	46910	
50773	52337	52456	54841	60666	64915	69787	73606	77226	81915	86098	92680	96923	105559	113391	
117551	124257	133171	142595	140728	149419	156923	162034	176068	187817	188726	199766	
208227	215760
*DO THE SAME FOR TAXES ON PRODUCTS AND IMPORTS NET OF SUBSIDIES
READ VTAXPIS / BYVAR
1458	1538	1462	1608	1712	1924	2068	2208	2424	2637	2938	3156	3543	4006	4974	6116	
7923	8979	9405	11017	12972	14799	16925	19285	22176	25517	28306	30613	35351	39437	42508	42874	
42351	43601	48488	52820	56950	59756	63147	66657	71114	80627	84742	92351	99161	101385	107644	
115014	122560	119714	128356	135223	138567	148094	157748	161261	170980	178796	186547
*TAKE THE DIFFERENCE
GENR VS = VTAXPI-VTAXPIS

*SWITCH TO BILLIONS OF DOLLARS FROM MILLIONS OF DOLLARS
SMPL 1 60
GENR VS=VS/1000
*WE WILL ASSUME THAT ALL OF THE PRODUCT SUBSIDIES FALL ON CONSUMPTION (WHICH IS NOT QUITE RIGHT BUT ...)
*CALCULATE THE SUBSIDY RATE SRC
SMPL 1 60
GENR SRC=0
SMPL 1 59
GENR SRC=VS/VC
*ASSUME THAT THE SUBSIDY RATE FOR 2018 IS EQUAL TO THE SUBSIDY RATE FOR 2017
SMPL 60 60
GENR SRC=SRC(59)
SMPL 1 60
GENR VS=SRC*VC
*PRINT OUT FINAL TABLE OF TAXES AND SUBSIDIES AND THE CORRESPONDING RATES
SMPL 1 60
FORMAT(F9.0,6F11.3)
PRINT YEAR VS TC TX TM TL TK / FORMAT
FORMAT(F8.0,6F15.5)
PRINT YEAR SRC TRC TRX TRM TRL TRK  / FORMAT
STAT SRC TRC TRX TRM TRL TRK
*SUBSIDY RATE INCREASED THROUGH THE 60S AND 70S, THEN STAYED AROUND 4% IN THE 80S AND 90S. IT DROPPED SUDDENLY IN 2000, AS THE AUSTRALIAN
*GOVERNMENT REFORMED THE ECONOMY, ALMOST ELIMINATING AGRICULTURAL SUBSIDIES. SEE Gary Banks (2006), "Structural reform Australian-style: lessons for 
*others" ON THAT TOPIC. THE SUBSIDY RATE THEN STAYED AROUND 3% IN THE 2000S AND 2010S.

*NOW WE CAN GENR GROSS PROFITS (OR GROSS RETURN TO CAPITAL) USING PRODUCER PRICES.
GENR GPROF=PC*(1-TRC+SRC)*QC+PI*QI+PG*QG+PX*(1-TRX)*QX-PM*(1+TRM)*QM-PL*QL
*IF WE SUBTRACT THE VALUE OF DEPRECIATION ALLOWANCES FROM GROSS PROFITS, WE OBTAIN NET 
*PROFITS OR NET INCOME BEFORE TAXES, NPROF:
GENR NPROF=GPROF-VD
FORMAT(F8.0,4F12.3)
PRINT YEAR GPROF NPROF VD VL / FORMAT
*NOW WE CAN GENR A PROFITS (OR INCOME) TAXATION RATE,TRKI:
GENR TRKI=TK/NPROF
FORMAT(F8.0,7F12.5)
PRINT YEAR TRC SRC TRX TRM TRL TRKI TRK / FORMAT
STAT TRC SRC TRX TRM TRL TRKI TRK
 *NAME        N    MEAN        ST. DEV      VARIANCE     MINIMUM      MAXIMUM
 *TRC          60  0.12044     0.13353E-01 0.17829E-03  0.99729E-01  0.14351
 *SRC          60  0.31178E-01 0.86755E-02 0.75264E-04  0.99237E-02  0.45331E-01
 *TRX          60  0.13495E-02 0.26855E-02 0.72118E-05  0.00000E+00  0.13513E-01
 *TRM          60  0.53943E-01 0.23330E-01 0.54428E-03  0.20097E-01  0.93596E-01
 *TRL          60  0.21312     0.44144E-01 0.19487E-02  0.12457      0.26960
 *TRKI         60  0.27765     0.41390E-01 0.17131E-02  0.21428      0.40318
 *TRK          60  0.25161E-01 0.66622E-02 0.44385E-04  0.16334E-01  0.42505E-01
*THE ABOVE NUMBERS LOOK REASONABLE AND SIMILAR TO THOSE FOR CANADA. 
*THE CONSUMPTION TAX RATE VARIED BETWEEN 10.0% AND 14.3%, THE SUBSIDY RATE VARIED BETWEEN 1.0% AND 4.5%, ITS MAXIMUM REACHED IN THE 80S AND 90S,
*BEFORE THE REFORM IN 2000.
*THE TAX RATE ON EXPORTS IS NEGLIGIBLE, EXCEPT DURING THE PERIOD IN THE 1970s WHEN IT HOVERED AROUND 1.0%, DUE TO A TAX ON EXPORTS OF GOLD.
*THE IMPORT TARIFF RATE IS IN GENERAL HIGHER THAN IN CANADA, EVEN TODAY (NO NAFTA). IT REACHED A MAXIMUM RATE OF 9.4% IN 1960 AND FELL 
*STEADILY TO 2.0% IN 2010, BUT REBOUNDED TO 3.9% RECENTLY.
*THE TAX RATE ON LABOUR INPUT STARTED LOW AT 12.3%, TRENDED UP TO 26.9% IN 1999 AND ENDED AT 24.8% IN 2018
*THE TAX RATE ON CAPITAL INCOME REACHES A LOW OF 21.4% IN 1983 AND A HIGH OF 40.3% IN 2000.
*THE TAX RATE ON CAPITAL AS A FRACTION OF CAPITAL ASSET VALUE STAYED AROUND 2.2% UNTIL THE 1990S, WHEN IT STARTED CLIMBING, REACHING A HIGH OF 4.3%
*IN 2007. IT DECREASED AFTER THE RECESSION, SETTLING AROUND 3.0%.
*SO TRL AND TRK INCREASED IN THE SAMPLE. TRM AND TRX DECREASED. TRC STAYED ABOUT CONSTANT.

*WE WILL NOW USE OUR DATA TO CALCULATE AN OVERALL INTERNAL REAL RATE OF RETURN THAT THE ECONOMY EARNS 
*EACH YEAR.
*HOWEVER, OUR NEGLECT OF LAND WILL TEND TO BIAS OUR RATES OF RETURN
*WE USE HICKS EDWARDS BELL APPROACH TO THE MEASUREMENT OF INCOME AND THE RETURN TO CAPITAL
*WE WILL NOW CALCULATE NOMINAL EX POST RATES OF RETURN ON CAPITAL EMPLOYED AT THE BEGINNING OF EACH YEAR.
*RECALL THAT OUR FORMULA FOR THE END OF PERIOD USER COST OF CAPITAL WAS PU=PK((1+R)-(1+IK)(1-D)) WHERE PK IS THE
*BEGINNING OF THE YEAR PRICE FOR A UNIT OF CAPITAL, R IS THE PRODUCTION UNIT'S COST OF FINANCIAL CAPITAL,
*D IS THE DEPRECIATION RATE AND IK IS THE ACTUAL OR ANTICIPATED ASSET INFLATION RATE. WE INITIALLY FOLLOW 
*JORGENSON AND USE ACTUAL OR EX POST ASSET INFLATION RATES.
*WE HAVE ALREADY GENERATED BEGINNING OF THE PERIOD CAPITAL STOCKS, QK, AND THE CORRESPONDING BEGINNING OF THE PERIOD
*PRICE OF CAPITAL PK. WE HAVE ALSO GENERATED PERIOD BY PERIOD GEOMETRIC DEPRECIATION RATES, D.
*GENR EX POST MEASURES OF ASSET PRICE INFLATION IK USING THE FORWARD LAG OPERATOR IN SHAZAM
SMPL 1 60
GENR IK=(LAG(PK,-1)/PK)-1
*NOW GENR THE CHANGE IN THE VALUE OF THE BEGINNING OF THE PERIOD CAPITAL STOCK OVER THE PERIOD AS IT DEPRECIATES.
*THE BEGINNING OF THE PERIOD VALUE OF THE CAPITAL STOCK IS PK*QK=VK; THE END OF PERIOD VALUE OF THE INITIAL CAPITAL 
*STOCK IS (1-D)*(1+IK)*VK. THUS DVK=VK-(1-D)*(1+IK)*VK. DVK INCORPORATES BOTH PHYSICAL DEPRECIATION AND MINUS POSSIBLE
*ASSET REVALUATION GAINS OVER THE PERIOD.  
SMPL 1 60
GENR DVK=VK-(1-D)*(1+IK)*VK
*NOTE THAT AT TIMES, DVK CAN BECOME NEGATIVE IF THE ASSET INFLATION RATE IS LARGER THAN THE DEPRECIATION RATE.
*RECALL THAT WE DEFINED THE GROSS PROFITS (OR GROSS OPERATING SURPLUS OF THE PRODUCTION SECTOR AT PRODUCER PRICES)
*AS GPROF. WE CAN SET GPROF EQUAL TO THE VALUE OF CAPITAL SERVICES WHICH IS VK*((1+R)-(1+IK)*(1-D))=VK*R+DVK=GPROF
*THUS THE EX POST NOMINAL RATE OF RETURN ON THE VALUE OF ASSETS R IS EQUAL TO R=(GPROF-DVK)/VK 
GENR R=(GPROF-DVK)/VK
FORMAT(F8.0,3F10.3,3F10.5)
PRINT YEAR GPROF VK DVK D IK R / FORMAT
STAT  GPROF VK DVK D IK R
 *NAME        N    MEAN        ST. DEV      VARIANCE     MINIMUM      MAXIMUM
 *GPROF        60   230.96      249.65      62323.       6.6994       864.71
 *VK           60   1345.4      1356.3     0.18395E+07   49.452       4722.4
 *DVK          60   65.319      85.019      7228.2      -18.575       290.13
 *D            60  0.62871E-01 0.65217E-02 0.42532E-04  0.52961E-01  0.72516E-01
 *IK           60  0.36697E-01 0.38807E-01 0.15060E-02 -0.21899E-01  0.14674
 *R            60  0.12659     0.34132E-01 0.11650E-02  0.53074E-01  0.22103
*THESE NOMINAL EX POST RATES OF RETURN R ARE HIGH DUE TO THE FACT THAT WE ARE NEGLECTING LAND AND OTHER 
*ASSETS LIKE NATURAL RESOURCE STOCKS IN OUR ASSET BASE. R FOR AUSTRALA IS A BIT LOWER THAN R FOR CANADA (AROUND 15%).

*NOTE THAT WE ARE INCLUDING EX POST ASSET CAPITAL GAINS AS PART OF THE RETURN TO CAPITAL AND SO IN YEARS
*WHEN ASSET INFLATION IK IS HIGH, THE EX POST RATE OF R IS ALSO VERY HIGH.
*THE ABOVE METHODOLOGY IS REASONABLE IF WE WANT TO DETERMINE RATES OF RETURN ON CAPITAL ASSUMING THAT 
*CAPITAL GAINS ON ASSETS ARE REALIZED AT THE END OF EACH PERIOD.BUT THE IK INFLATION RATES CAN BE VERY
*VARIABLE SO IN ORDER TO CONSTRUCT USER COSTS, WE WILL SMOOTH THESE IK FACTORS USING A NONPARAMETRIC
*SMOOTHING METHOD IN SHAZAM.THESE SMOOTHED IK ESTIMATES ARE MORE SUITABLE TO USE IN A USER COST FORMULA
*THAT IS SUPPOSED TO REPRESENT RENTAL PRICES FOR CAPITAL SERVICES (WHICH ARE RELATIVELY SMOOTH WHEN THEY EXIST).
*NOW GENR SMOOTHED IK; WE NEED TO CHOOSE A SMOOTHING PARAMETER WHICH IS CALLED "SMOOTH" SO WE WILL TRY VARIOUS
*VALUES FOR "SMOOTH" AND SEE WHAT HAPPENS
NONPAR IK T / METHOD=LOWESS SMOOTH=.13 LIST PREDICT=IKS13
NONPAR IK T / METHOD=LOWESS SMOOTH=.15 LIST PREDICT=IKS15
NONPAR IK T / METHOD=LOWESS SMOOTH=.17 LIST PREDICT=IKS17

NONPAR IK T / METHOD=LOWESS SMOOTH=.20 LIST PREDICT=IKS20
NONPAR IK T / METHOD=LOWESS SMOOTH=.25 LIST PREDICT=IKS25
NONPAR IK T / METHOD=LOWESS SMOOTH=.30 LIST PREDICT=IKS30
NONPAR IK T / METHOD=LOWESS SMOOTH=.35 LIST PREDICT=IKS35
NONPAR IK T / METHOD=LOWESS SMOOTH=.40 LIST PREDICT=IKS40
FORMAT(F8.0,9F10.5)
PRINT YEAR IK IKS13 IKS15 IKS17 IKS20 IKS25 IKS30 IKS35 IKS40 / FORMAT
*HERE ARE THE CROSS VALIDATION SCORES FOR THE ABOVE SMOOTHING MODELS
* SMOOTHING PARAMETER  CV SCORE    R SQUARE BETWEEN ACTUAL AND PREDICTED
*    .13               .000448       .8510
*    .15               .000484       .8036
*    .17               .000527       .7611
*    .20               .000554       .7301
*    .25               .000564       .7118
*    .30               .000571       .6937
*    .35               .000579       .6833
*    .40               .000600       .6665
*THE SMOOTHING PARAMETER IS THE FRACTION OF THE SAMPLE THAT IS USED IN A WEIGHTED LOCAL REGRESSION TO 
*PREDICT A DEPENDENT VARIABLE USING THE X OBSERVATIONS THAT ARE CLOSEST TO THE X FOR A GIVEN OBSERVATION.
*THE METHOD IS DUE TO W.S. CLEVELAND (1979),"Robust Locally Weighted Regression and Smoothing Scatterplots",
*Journal of the American Statistical Association 74, 829-836. THE SMALLER IS THIS FRACTION, THE FEWER
*LOCAL OBSERVATIONS ARE USED IN THE LOCAL REGRESSIONS. THE CROSS VALIDATION SCORE IS A MEASURE OF HOW
*WELL THE METHOD PREDICTS EACH OBSERVATION SO THE LOWER THE SCORE, THE BETTER ARE THE PREDICTIONS. I
*AM RELUCTANT TO DECREASE THE SMOOTHING PARAMETER BELOW .13, BECAUSE LOWER SMOOTHS WILL MAKE THE R SQUARE
*CLOSER TO 1 AND HENCE, WE ARE NOT DOING MUCH SMOOTHING. THUS I WILL TAKE IKS13 AS MY PREDICTED CAPITAL
*PRICE INFLATION RATE SERIES.

GENR IK=IKS13
*NOW REPEAT THE ABOVE STATEMENTS TO GET A NEW R SERIES THAT SHOULD BE SMOOTHER THAN OUR INITIAL R SERIES
GENR DVK=VK-(1-D)*(1+IK)*VK
GENR R=(GPROF-DVK)/VK
FORMAT(F8.0,3F10.3,3F10.5)
PRINT YEAR GPROF VK DVK D IK R / FORMAT
STAT  GPROF VK DVK D IK R 
 *NAME        N    MEAN        ST. DEV      VARIANCE     MINIMUM      MAXIMUM
 *GPROF        60   230.96      249.65      62323.       6.6994       864.71
 *VK           60   1345.4      1356.3     0.18395E+07   49.452       4722.4
 *DVK          60   65.319      85.019      7228.2      -18.575       290.13
 *D            60  0.62871E-01 0.65217E-02 0.42532E-04  0.52961E-01  0.72516E-01
 *IK           60  0.36697E-01 0.38807E-01 0.15060E-02 -0.21899E-01  0.14674
 *R            60  0.12659     0.34132E-01 0.11650E-02  0.53074E-01  0.22103

*FOR AUSTRALIA, SMOOTHING THE IK DID NOT MAKE MUCH DIFFERENCE.
***

*IT IS ALSO USEFUL TO CALCULATE REAL RATES OF RETURN FOR THE AUSTRALIAN ECONOMY
*TO CALCULATE REAL RATES OF RETURN, WE NEED CONSUMER PRICE INDEX INFLATION RATES 
*I GOT BEGINNING OF PERIOD CPI (1959-2019) FROM ABS WEBSITE
SMPL 1 61
READ CPI / BYVAR
7.3	7.6	7.9	7.8	7.8	8	8.3	8.6	8.9	9.1	9.4	9.7	10.2	10.9	11.8	13.5	15.8	17.7	20.1	21.7	23.6	
26.2	28.4	31.5	35	36.4	38.8	42.1	46	49.3	53	57.1	59	59.7	60.8	61.9	64.7	66.7	66.9	
67.4	68.1	70.2	74.5	76.6	78.6	80.6	82.6	85.9	87.7	91.6	92.9	95.8	99.2	100.4	102.8	105.9	
107.5	108.6	110.7	113	114.882773
*GENR ANNUAL CPI INFLATION RATES I
SMPL 1 60
GENR I=(LAG(CPI,-1)/CPI)-1
*GENR REAL EX POST RATES OF RETURN FOR THE AUSTRALIAN ECONOMY
GENR RR=((1+R)/(1+I))-1
*WE GENERATE PC INFLATION RATES FOR THE YEARS 1961-2018 TO COMPARE WITH THE CPI INFLATION RATES DEFINED ABOVE
SMPL 2 60
GENR IPC=(PC/LAG(PC))-1

FORMAT(F8.0,3F10.5)
PRINT YEAR IK I IPC / FORMAT
STAT IK I IPC / PCOR
*GRAPH(I,IPC,IK,YEAR)
 *NAME        N    MEAN        ST. DEV      VARIANCE     MINIMUM      MAXIMUM
 *IK           59  0.37429E-01 0.33489E-01 0.11215E-02 -0.31919E-02  0.12888
 *I            59  0.47806E-01 0.39148E-01 0.15326E-02 -0.12658E-01  0.17037
 *IPC          59  0.47376E-01 0.38047E-01 0.14476E-02  0.55225E-02  0.17582

 *CORRELATION MATRIX OF VARIABLES -       59 OBSERVATIONS
 *IK         1.0000
 *I         0.90988       1.0000
 *IPC       0.94224      0.94538       1.0000
 *            IK           I            IPC

*IT CAN BE SEEN THAT THE PC INFLATION RATES IPC ARE HIGHLY CORRELATED WITH THE CPI INFLATION RATES I (.94538 COR COEF).
*THESE TWO INFLATION RATES ARE ALSO HIGHLY CORRELATED WITH THE CAPITAL STOCK INFLATION RATE IK.  
SMPL 1 60
FORMAT(F8.0,5F10.5)
*GRAPH(RR,YEAR)
PRINT YEAR RR R I IK TRK / FORMAT
STAT RR R I IK TRK
 *NAME        N    MEAN        ST. DEV      VARIANCE     MINIMUM      MAXIMUM
 *NAME        N    MEAN        ST. DEV      VARIANCE     MINIMUM      MAXIMUM
 *RR           60  0.75941E-01 0.24582E-01 0.60427E-03  0.19226E-01  0.12441
 *R            60  0.12657     0.28219E-01 0.79633E-03  0.70348E-01  0.19368
 *I            60  0.47694E-01 0.38825E-01 0.15073E-02 -0.12658E-01  0.17037
 *IK           60  0.36678E-01 0.33710E-01 0.11363E-02 -0.76329E-02  0.12888
 *TRK          60  0.25161E-01 0.66622E-02 0.44385E-04  0.16334E-01  0.42505E-01

*THE AVERAGE (SMOOTHED) NOMINAL RATE OF RETURN ON CAPITAL STOCK ASSETS R IS 12.7% PER YEAR AND THE AVERAGE REAL RATE OF 
*RETURN RR IS 7.6%. THESE RATES ARE OVERSTATED DUE TO OUR NEGLECT OF LAND, INVENTORY AND NATURAL RESOURCE ASSETS IN OUR 
*ASSET BASE. THEY ARE COMPARABLE (BIT LOWER) TO THOSE OF CANADA. OUR SLIGHTLY SMOOTHED REAL RATE OF RETURN SERIES, RR, IS AN IMPORTANT INDICATOR 
*OF THE EFFICIENCY OF AN ECONOMY. RR CLIMBED DURING THE 1990S AND THE EARLY 2000S AND HAS BEEN AROUND 10% SINCE.
*NOTE ALSO THAT THIS IS A BEFORE TAX RATE OF RETURN AND THE AVERAGE ASSET TAX RATE IS 2.5% SO THE AVERAGE
*AFTER TAX REAL RATE OF RETURN IS 5.1% (WHICH IS STILL TOO HIGH BECAUSE OF THE NARROW ASSET BASE).

*WE CAN USE THE INTERNAL RATE OF RETURN THAT WE HAVE JUST DEFINED AS AN APPROXIMATION TO THE COST OF CAPITAL
*AND GENERATE AN EX POST USER COST OF CAPITAL PU:
SMPL 1 60
GENR PU=PK*((1+R)-(1+IK)*(1-D))
*GENR THE VALUE OF CAPITAL SERVICES
GENR VKS=PU*QK
*NORMALIZE THE USER COST
GENR CON=PU(1)
GENR PKS=PU/CON
GENR QKS=VKS/PKS
*WE DEFINED GROSS PROFITS EARLIER AS GPROF=PC*(1-TRC+SRC)*QC+PI*QI+PG*QG+PX*(1-TRX)*QX-PM*(1+TRM)*QM-PL*QL
*IF WE ADD PL*QL TO GROSS PROFITS, WE OBTAIN AN ESTIMATE OF GDP AT PRODUCER PRICES. WE WILL CALL THIS VY
*WHICH REPLACES OUR EARLIER VY WHICH WAS GDP AT FINAL DEMAND PRICES.
GENR VY=PC*(1-TRC+SRC)*QC+PI*QI+PG*QG+PX*(1-TRX)*QX-PM*(1+TRM)*QM
*CHECK TO SEE IF VY=VL+VKS
GENR CHECK=VY-VL-VKS
FORMAT(F8.0,4F11.3,3F10.5)
PRINT YEAR VL QL VKS QKS PL PKS CHECK / FORMAT
*   YEAR        VL         QL         VKS       QKS       PL        PKS      CHECK
*   1959.      8.708      8.708      6.699      6.699   1.00000   1.00000   0.00000
*   1960.      9.376      9.062      7.011      7.022   1.03473   0.99843   0.00000
*   1961.      9.678      9.079      7.158      7.404   1.06598   0.96678   0.00000
*   1962.     10.257      9.672      7.786      7.681   1.06044   1.01364  -0.00000
*   1963.     11.217     10.185      8.879      8.047   1.10129   1.10340   0.00000
*   1964.     12.594     10.735      9.250      8.444   1.17316   1.09549   0.00000
*   1965.     13.600     10.996      9.527      8.924   1.23677   1.06764  -0.00000
*   1966.     14.823     11.394     10.974      9.397   1.30090   1.16782   0.00000
*   1967.     16.056     11.635     11.789      9.931   1.37995   1.18705   0.00000
*   1968.     17.706     11.732     13.360     10.475   1.50922   1.27541  -0.00000
*   1969.     19.855     11.953     15.030     11.130   1.66106   1.35034  -0.00000
*   1970.     22.786     12.332     15.457     11.740   1.84762   1.31668  -0.00000
*   1971.     25.432     12.592     16.877     12.375   2.01961   1.36385   0.00000
*   1972.     28.399     12.774     19.109     12.962   2.22316   1.47417   0.00000
*   1973.     34.686     13.150     21.623     13.527   2.63777   1.59852  -0.00000
*   1974.     44.956     13.265     21.866     14.250   3.38900   1.53448  -0.00000
*   1975.     52.030     13.194     26.627     14.839   3.94336   1.79440  -0.00000
*   1976.     59.069     13.255     32.141     15.413   4.45647   2.08532   0.00000
*   1977.     65.115     13.417     34.905     16.068   4.85301   2.17230   0.00000
*   1978.     69.728     13.462     43.124     16.646   5.17965   2.59071  -0.00000
*   1979.     78.410     13.868     49.014     17.414   5.65415   2.81460   0.00000
*   1980.     90.575     14.237     53.398     18.152   6.36204   2.94178   0.00000
*   1981.    104.124     14.354     61.867     19.027   7.25412   3.25157  -0.00000
*   1982.    113.924     13.993     65.421     20.030   8.14129   3.26620  -0.00000
*   1983.    122.932     13.902     77.628     20.656   8.84243   3.75817   0.00000
*   1984.    135.261     14.493     84.397     21.442   9.33288   3.93607   0.00000
*   1985.    148.834     15.044     96.191     22.389   9.89331   4.29631   0.00000
*   1986.    161.994     15.477    109.936     23.345  10.46696   4.70919  -0.00000
*   1987.    177.725     15.935    128.122     24.156  11.15281   5.30402  -0.00000
*   1988.    198.357     16.570    145.962     25.113  11.97067   5.81211   0.00000
*   1989.    221.549     17.354    155.995     26.321  12.76681   5.92675   0.00000
*   1990.    232.465     16.831    157.103     27.518  13.81157   5.70909   0.00000
*   1991.    236.141     16.522    167.821     28.232  14.29268   5.94434  -0.00000
*   1992.    245.895     16.480    178.096     28.759  14.92097   6.19268  -0.00000
*   1993.    255.560     17.190    185.128     29.426  14.86704   6.29127  -0.00000
*   1994.    270.358     17.970    196.214     30.180  15.04484   6.50138   0.00000
*   1995.    289.787     18.250    207.350     31.218  15.87896   6.64199   0.00000
*   1996.    308.848     18.291    215.396     32.233  16.88546   6.68257   0.00000
*   1997.    321.836     18.617    233.907     33.386  17.28719   7.00619   0.00000
*   1998.    340.898     18.776    244.541     34.666  18.15573   7.05421   0.00000
*   1999.    358.115     19.429    266.382     36.052  18.43192   7.38883  -0.00000
*   2000.    382.497     19.709    272.746     37.547  19.40703   7.26417   0.00000
*   2001.    403.611     19.602    296.340     38.587  20.58987   7.67985  -0.00000
*   2002.    426.416     20.117    314.934     39.859  21.19647   7.90116   0.00000
*   2003.    453.512     20.398    344.668     41.478  22.23328   8.30968   0.00000
*   2004.    488.136     21.063    370.269     43.451  23.17486   8.52151   0.00000
*   2005.    521.445     21.577    407.758     45.562  24.16659   8.94951  -0.00000
*   2006.    566.964     22.198    449.801     47.783  25.54128   9.41351  -0.00000
*   2007.    616.634     22.814    486.868     50.137  27.02829   9.71069   0.00000
*   2008.    640.955     23.007    546.036     52.899  27.85875  10.32228   0.00000
*   2009.    667.384     22.960    556.256     55.447  29.06747  10.03213   0.00000
*   2010.    725.935     23.593    608.629     57.903  30.76927  10.51113  -0.00000
*   2011.    771.191     24.062    645.330     60.618  32.04965  10.64578   0.00000
*   2012.    795.613     24.186    651.561     63.892  32.89573  10.19783   0.00000
*   2013.    824.400     24.274    680.103     67.115  33.96291  10.13347   0.00000
*   2014.    849.013     24.644    683.830     69.934  34.45058   9.77819  -0.00000
*   2015.    875.385     25.114    687.739     72.354  34.85585   9.50519  -0.00000
*   2016.    896.218     25.496    765.794     74.377  35.15085  10.29608   0.00000
*   2017.    932.738     26.260    805.349     76.287  35.51986  10.55688   0.00000
*   2018.    973.325     26.903    864.707     78.402  36.17887  11.02915   0.00000
*CHECK IS SATISFIED. THE NUMBERS LOOK HIGH BUT PLAUSIBLE. WAGE RATE GREW 36.2 FOLD AND THE PRICE OF CAPITAL SERVICES
*GREW 11 FOLD OVER THE SAMPLE PERIOD. 
SMPL 1 1
GENR GROWTHL=QL(60)/QL(1)
GENR GROWTHK=QKS(60)/QKS(1)
PRINT GROWTHL GROWTHK
*LABOUR INPUT GREW 3.1 FOLD AND CAPITAL SERVICES INPUT GREW 11.7 FOLD OVER THE SAMPLE PERIOD.
*CALCULATE THE SHARES OF LABOUR AND CAPITAL IN GDP AT PRODUCER PRICES

SMPL 1 60
GENR SL=VL/VY
GENR SK=VKS/VY
FORMAT(F8.0,2F10.5)
PRINT YEAR SL SK / FORMAT
STAT SL SK
 *NAME        N    MEAN        ST. DEV      VARIANCE     MINIMUM      MAXIMUM
 *SL           60  0.58344     0.31369E-01 0.98399E-03  0.52955      0.67277
 *SK           60  0.41656     0.31369E-01 0.98399E-03  0.32723      0.47045
*THE LABOUR SHARE STARTS AT .57, GROWS TO .67 IN 1974 AND THEN FALLS TO .53 IN 2018.
*THE LABOUR SHARE IS ALMOST THE SAME IN AUSTRALIA THAN IN CANADA.

***
*LET US USE PC AS OUR PRICE OF CONSUMPTION AND CALCULATE BEFORE TAX REAL WAGES
GENR REALW=PL/PC
FORMAT(F8.0,3F10.5)
PRINT YEAR REALW PC PL / FORMAT
*THUS REAL WAGES IN AUSTRALIA GREW ABOUT 2.45 FOLD OVER THE SAMPLE PERIOD. 

***
*LABOUR AND CAPITAL SERVICES ARE OUR TWO PRIMARY INPUTS
*WE WILL FINISH UP THIS FILE BY COMPUTING (CONVENTIONAL) TOTAL FACTOR PRODUCTIVITY GROWTH
*WE WILL USE CHAINED FISHER INDEXES TO DO THE AGGREGATION HERE
*CONSTRUCT AN INPUT AGGREGATE USING THE CHAINED FISHER OPTION IN SHAZAM
SMPL 1 60
INDEX PL QL PKS QKS / CHAIN FISHER=PZ QFISHER=QZ
*GENR THE VALUE OF INPUT VZ
GENR VZ=PZ*QZ
*NOW CONSTRUCT AN OUTPUT AGGREGATE. WE FIRST CONSTRUCT PRODUCER PRICES FOR OUTPUTS
GENR PPC=PC*(1-TRC+SRC)
GENR PPG=PG
GENR PPI=PI
GENR PPX=PX*(1-TRX)
GENR PPM=PM*(1+TRM)
*CONSTRUCT AN OUTPUT AGGREGATE (AGAIN SWITCH QM TO -QM)
GENR QM=-QM
FORMAT(F8.0,7F11.5)
PRINT YEAR PPC PPG PPI PPX PPM PL PKS / FORMAT
FORMAT(F8.0,7F11.3)
PRINT YEAR QC QG QI QX QM QL QKS / FORMAT
INDEX PPC QC PPG QG PPI QI PPX QX PPM QM / CHAIN FISHER=PY QFISHER=QY
GENR PROD=QY/QZ
GENR CHECK=PY*QY-PZ*QZ
FORMAT(F8.0,2F12.3,2F10.5)
PRINT YEAR QY QZ PROD CHECK / FORMAT
*CHECK SHOULD BE 0 AND IT IS
*SINCE WE ARE USING BALANCING RATES OF RETURN, QY SHOULD EQUAL QZ FOR OBSERVATION 1 AND THEY ARE. THIS PROVIDES A CHECK ON YOUR DATA.
*NOTE THAT PRODUCTIVITY ENDED UP AT 1.50 AT THE END OF THE SAMPLE PERIOD. 
*PRODUCTIVITY PEAKED IN 2003 AT 1.54 AND HAS FALLEN SLIGHTLY SINCE.
 
***
*CONSTRUCT THE ARITHMETIC AVERAGE OF TFP GROWTH RATES OVER THE SAMPLE PERIOD
SMPL 2 60
GENR YG=QY/LAG(QY)
GENR ZG=QZ/LAG(QZ)
GENR PRODG=YG/ZG
FORMAT(F8.0,3F15.5)
PRINT YEAR PRODG YG ZG
STAT PRODG YG ZG
*AVERAGE TFP GROWTH RATES SHOULD BE IN THE RANGE OF .5% TO 2.5% PER YEAR WITH AN AVERAGE AROUND 1%.
*AUSTRALIA'S ARITHMETIC AVERAGE TFP GROWTH RATE IS 0.70% PER YEAR WHICH IS MODERATLY LOW AND SIMILAR TO CANADA (0.72%).
*BUT THE UNDERLYING DYNAMIC IS DIFFERENT. BOTH YG AND QG ARE HIGHER IN AUSTRALIA THAN IN CANADA.

*DO DECADE BY DECADE RATES OF TFP GROWTH
SMPL 2 10
STAT PRODG YG ZG
*THE AVERAGE RATE OF TFP GROWTH IN THE 1960'S WAS 1.15% PER YEAR WHICH IS PRETTY GOOD (BUT LOWER THAN CANADA). MOST OECD COUNTRIES
*HAD VERY HIGH RATES OF TFP GROWTH IN THE 1960S, WHICH TAILED OFF IN SUBSEQUENT DECADES. WHY IS THIS?
*THIS IS STILL A PUZZLE. THE HIGH UNANTICIPATED INFLATION WHICH STARTED IN 1973 IS ONE POSSIBLE EXPLANATORY
*FACTOR. THIS CAN LEAD TO HIGH REAL INTEREST RATES WHICH PERSISTED UNTIL THE MIDDLE OF THE 1990S.
*THERE DOES SEEM TO BE MORE TFP GROWTH WHEN INFLATION IS LOW. WHY? PERHAPS DUE TO THE BUSINESS INCOME TAXATION SYSTEM
*WHICH DOES NOT INDEX DEPRECIATION ALLOWANCES FOR INFLATION. ANOTHER THEORY LOOKS AT THE GROWTH OF THE 
*GOVERNMENT SECTOR WHICH TOOK PLACE IN THE 1970S AND 1980S WHICH WERE ACCOMPANIED BY HIGHER TAXES AND HENCE
*MORE DEADWEIGHT LOSS. ALSO, REMEMBER THAT THE GOVT SECTOR WILL NOT SHOW ANY PRODUCTIVITY INCREASES DUE
*TO NATIONAL INCOME ACCOUNTING CONVENTIONS SO THE LARGER GOVT SECTORS WILL NATURALLY LEAD TO LOWER TFP GROWTH.
*RECALL THAT GOVT STATISTICIANS DO NOT COUNT ANY INTEREST ON GOV CAPITAL AS PART OF INPUT SO THIS WILL
*ALSO BE MISSING FROM THE VALUE OF GOVT OUTPUT. BUT OUR IMPUTATION METHOD HAS IMPUTED AN INTEREST RATE TO GOVT CAPITAL 
*AS PART OF INPUT. SO OUR METHODOLOGY MAY LEAD TO MEASURED PRODUCTIVITY DECREASES AS THE SIZE
*OF THE GOVT SECTOR INCREASES. A FINAL EXPLANATORY FACTOR HAS TO DO WITH THE MOVEMENT OF ECONOMIES AWAY FROM GOODS
*TO SERVICES, WHICH ARE HARDER TO MEASURE; E.G., BANKING SECTOR OUTPUT, INSURANCE, GAMBLING, ADVERTISING, TELECOM, ETC.
*MANY RESEARCHERS (INCLUDING DIEWERT AND FOX) FEEL THAT THERE IS UNDERMEASUREMENT OF OUTPUT DUE TO THIS FACTOR.
SMPL 11 20
STAT PRODG YG ZG
*THE AVERAGE RATE OF TFP GROWTH IN THE 1970'S WAS 1.20% PER YEAR; A BIT HIGHER THAN AVERAGE FOR OECD COUNTRIES (CANADA=0.96%).
SMPL 21 30
STAT PRODG YG ZG
*THE AVERAGE RATE OF TFP GROWTH IN THE 1980'S WAS 0.54% PER YEAR, WHICH IS LOW LIKE CANADA (0.48%).
SMPL 31 40
STAT PRODG YG ZG
*THE AVERAGE RATE OF TFP GROWTH IN THE 1990'S WAS 1.20% PER YEAR, WHICH IS HIGH (CANADA HAD 0.9%).
SMPL 41 50
STAT PRODG YG ZG
*THE AVERAGE RATE OF TFP GROWTH IN THE 2000'S WAS 0.16% PER YEAR, WHICH IS LOW BUT BETTER THAN CANADA (-0.26%).
SMPL 51 60
STAT PRODG YG ZG
*THE AVERAGE RATE OF TFP GROWTH IN THE 2010S IS -0.01% WHICH IS BAD, SIMILAR TO CANADA'S STAGNATION (0.09%).

***
*NOW CONSTRUCT SOME DATA FILES FOR LATER ECONOMETRIC ESTIMATION EXERCISES
*SET UP A PRODUCER DATA FILE FOR RUNNING THE PMOD.TXT REGRESSIONS AND FOR DOING A PRODUCTIVITY DECOMPOSITION 
*INTO EXPLANATORY FACTORS AS IS DONE IN DIEWERT AND MORRISON, ECONOMIC JOURNAL, 1986
*FOR OUR FIRST FILE, WE WILL HAVE C,I,G,X,-M AND -L AS VARIABLE NET OUTPUTS AND K AS THE FIXED INPUT
SMPL 1 60
GENR Y1=QC
GENR Y2=QG
GENR Y3=QI
GENR Y4=QX
GENR Y5=QM
*NOTE;QM IS NEGATIVE AT THIS POINT SO Y5 SHOULD BE NEGATIVE
GENR Y6=-QL
GENR P1=PPC
GENR P2=PPG
GENR P3=PPI
GENR P4=PPX
GENR P5=PPM
GENR P6=PL
FORMAT(F8.0,7F11.4)
PRINT YEAR Y1-Y6 QKS / FORMAT 
*NOTE Y5 AND Y6 SHOULD BE NEGATIVE
GENR CHECK=P1*Y1+P2*Y2+P3*Y3+P4*Y4+P5*Y5+P6*Y6-PKS*QKS
FORMAT(F8.0,8F10.5)
PRINT YEAR P1-P6 PKS / FORMAT
***
*FOR THE PRODUCTIVITY FILE, PROD.TXT, WE WILL WANT A DECOMPOSITION OF THE GROSS USER COST INTO VARIOUS COMPONENTS
*SO WE WILL ADD SOME EXTRA DATA SERIES
SMPL 1 61
FORMAT(F8.0,F10.5,2F10.4)
PRINT YEAR PK QK VK / FORMAT
SMPL 1 60
FORMAT(F8.0,5F11.5)
PRINT YEAR PC R TRK D IK / FORMAT
*RECALL THAT PC IS OUR FINAL EXPENDITURE DEFLATOR FOR HOUSEHOLD CONSUMPTION; IT IS CLOSE TO THE CANADIAN CPI
***
*WE GENR DATA FOR THE CONSUMER MODELS
SMPL 1 60
PRINT YEAR PC PL QC QL / FORMAT
*PC HAS COMMODITY TAXES IN THESE PRICES SO THEY DO NOT NEED TO BE ADJUSTED
*PL IS CORRECT FOR EMPLOYERS BUT NOT FOR WORKERS; WE NEED TO TAKE OUT TAXES
GENR PL=PL*(1-TRL)
*NORMALIZE NEW PRICE TO 1 IN BASE PERIOD
GENR CON=PL(1)
GENR PL=PL/CON
GENR QL=QL*CON
*CONVERT TO PER POP DATA
GENR X1=1000*QC/POP
GENR X2=1000*QL/POP
GENR PC1=PC
GENR PC2=PL
FORMAT(F8.0,4F15.5)
PRINT YEAR X1 X2 PC1 PC2 / FORMAT
*THE ABOVE TABLE OF DATA WILL BE READ INTO THE CMOD PROGRAMS  
*NOTE THAT AFTER TAX WAGES DID NOT GROW NEARLY AS QUICKLY AS BEFORE TAX WAGE RATES
*X1 IS PER POP CONSUMPTION; X2 IS PER POP LABOUR SUPPLY
***
*WE NEED A SERIES FOR POP IN PROD SO WE PRINT IT OUT HERE
FORMAT(F6.0,F15.1)
PRINT YEAR POP / FORMAT

SMPL 1 60
WRITE(aus_data_1) YEAR QC QG QI QX  
WRITE(aus_data_1) QM PC PG PI PX   / APPEND NOREWIND
WRITE(aus_data_1) PM TOFT D KYR    / APPEND NOREWIND
WRITE(aus_data_1) VKYR QE QSE HOURS  / NOREWIND APPEND
WRITE(aus_data_1) PL QL REALW TRC   / NOREWIND APPEND
WRITE(aus_data_1) SRC TRX TRM TRL    / NOREWIND APPEND
WRITE(aus_data_1) TRKI TRK GPROF QK  / NOREWIND APPEND
WRITE(aus_data_1) PK DVK D IK / NOREWIND APPEND
WRITE(aus_data_1) I  R RR  / NOREWIND APPEND
WRITE(aus_data_1) PY QY SL SK  / NOREWIND APPEND
WRITE(aus_data_1) QKS VKS QZ PZ  / NOREWIND APPEND
WRITE(aus_data_1) PROD YG ZG PRODG / NOREWIND APPEND

STOP   